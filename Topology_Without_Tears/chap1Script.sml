open HolKernel Parse boolLib bossLib;

open pred_setTheory
(* inherit from existing topology theory *)
open topologyTheory
open arithmeticTheory

val _ = new_theory "chap1";

(*

        open_in : 'a topology -> 'a set set
        topology : 'a set set -> 'a topology
        topspace : 'a topology -> 'a set


Not every set of sets corresponds to a topology, but the topology
function is total, so if it is passed a set that doesn't correspond to
a topology, it's behaviour is unspecified.

â€œopen_in t sâ€     "s is an open set in topology t"

     or

â€œs âˆˆ open_in tâ€

     or maybe

â€œs âˆˆ openSets tâ€

*)

Theorem SING_INTER[local]:
  {a} âˆ© (a INSERT B) = {a} âˆ§ (a INSERT B) âˆ© {a} = {a}
Proof
  csimp[EXTENSION]
QED

Theorem INTER_EQ[local]:
  (x âˆ© y = x â‡” x âŠ† y) âˆ§ (y âˆ© x = x â‡” x âŠ† y)
Proof
  simp[SUBSET_DEF, EXTENSION] >> metis_tac[]
QED

Theorem POW_INSERT':
  POW (x INSERT s) = POW s âˆª { x INSERT t | t âˆˆ POW s }
Proof
  simp[Once EXTENSION, POW_INSERT] >> metis_tac[]
QED

Theorem SUBSET_INSERT':
  A âŠ† e INSERT B â‡” âˆƒD. D âŠ† B âˆ§ (A = D âˆ¨ A = e INSERT D)
Proof
  dsimp[SUBSET_DEF] >> simp[EXTENSION] >> eq_tac >> rw[]
  >- (Cases_on â€˜e âˆˆ Aâ€™
      >- (disj2_tac >> qexists_tac â€˜A DELETE eâ€™ >> simp[] >> metis_tac[]) >>
      metis_tac[])
  >- metis_tac[]
  >- metis_tac[]
QED

Theorem pow6[local] =
        (SIMP_CONV (srw_ss()) [POW_EQNS] THENC
         SIMP_CONV (srw_ss()) [LET_THM, GSYM INSERT_SING_UNION,
                               INSERT_UNION_EQ])
        â€œPOW {a;b;c;d;e;f}â€

Theorem pow3[local] =
        (SIMP_CONV (srw_ss()) [POW_EQNS] THENC
         SIMP_CONV (srw_ss()) [LET_THM, GSYM INSERT_SING_UNION,
                               INSERT_UNION_EQ])
        â€œPOW {a;b;c}â€

Theorem example1_1_2:
  (* does it matter if elements a..f are distinct? Turns out: no *)
  istopology {{a;b;c;d;e;f}; âˆ…; {a}; {c;d}; {a;c;d}; {b;c;d;e;f}}
Proof
  simp[istopology] >> reverse (rw[]) >> simp[SING_INTER, INTER_EQ]
  >- (gvs[SUBSET_INSERT'] >> ONCE_REWRITE_TAC [EXTENSION] >> simp[] >>
      metis_tac[]) >>
  simp[EXTENSION] >> metis_tac[]
QED

Theorem example1_1_3:
  ALL_DISTINCT [a;b;c;d;e] â‡’
  Â¬istopology {{a;b;c;d;e};âˆ…;{a};{c;d};{a;c;e};{b;c;d}}
Proof
  simp[istopology] >> strip_tac >> disj2_tac >>
  qexists_tac â€˜{{c;d}; {a;c;e}}â€™ >> simp[SUBSET_DEF] >> rw[] >>
  ONCE_REWRITE_TAC[EXTENSION] >> simp[] >> metis_tac[NOT_INSERT_EMPTY]
QED

Theorem example1_1_4:
  ALL_DISTINCT[a;b;c;d;e;f] â‡’
  Â¬istopology {{a;b;c;d;e;f}; âˆ…; {a}; {f}; {a;f}; {a;c;f}; {b;c;d;e;f}}
Proof
  simp[istopology] >> strip_tac >> disj1_tac >>
  qexistsl_tac [â€˜{a;c;f}â€™, â€˜{b;c;d;e;f}â€™] >> simp[SING_INTER, INTER_EQ] >>
  simp[EXTENSION] >> metis_tac[]
QED

Theorem example1_1_5:
  Â¬istopology (univ(:num) INSERT {s | FINITE s})
Proof
  simp[istopology] >> disj2_tac >>
  qexists_tac â€˜{{n} | 2 â‰¤ n }â€™ >>
  simp[SUBSET_DEF, PULL_EXISTS] >> rw[] >>
  ONCE_REWRITE_TAC [EXTENSION] >> simp[]
  >- (qexists_tac â€˜1â€™ >> gen_tac >> Cases_on â€˜1 âˆˆ sâ€™ >> simp[] >> gen_tac >>
      Cases_on â€˜2 â‰¤ nâ€™ >> simp[] >> strip_tac >> gvs[]) >>
  strip_tac >>
  â€˜INJ (Î»n. {n + 2}) UNIV {{n} | 2 â‰¤ n}â€™
    by simp[INJ_IFF] >>
  drule INFINITE_INJ >> simp[]
QED

Theorem EMPTY_is_topology:
  istopology {âˆ…}
Proof
  simp[istopology, SUBSET_SING]
QED

Definition discrete_topology_def:
  discrete_topology X = topology (POW X)
End

Theorem openSets_discrete[simp]:
  open_in (discrete_topology X) s â‡” s âŠ† X
Proof
  â€˜open_in (discrete_topology X) = POW Xâ€™ suffices_by simp[] >>
  simp[discrete_topology_def] >> simp[GSYM (cj 2 topology_tybij)] >>
  simp[istopology] >> conj_tac
  >- simp[IN_POW, SUBSET_DEF] >>
  simp[SUBSET_DEF, IN_POW]>> metis_tac[]
QED

Definition indiscrete_topology_def:
  indiscrete_topology X = topology {âˆ…; X}
End

Theorem openSets_indiscrete[simp]:
  open_in (indiscrete_topology X) s â‡” s = X âˆ¨ s = âˆ…
Proof
  â€˜open_in (indiscrete_topology X) = {âˆ…; X}â€™
    suffices_by (simp[] >> metis_tac[]) >>
  simp[indiscrete_topology_def] >> simp[GSYM (cj 2 topology_tybij)] >>
  simp[istopology] >> rw[] >> simp[] >>
  â€˜k âˆˆ POW {âˆ…; X}â€™ by simp[IN_POW] >> pop_assum mp_tac >>
  simp[POW_EQNS] >> rw[] >> simp[]
QED

Theorem topspace_indiscrete[simp]:
  topspace (indiscrete_topology X) = X
Proof
  simp[topspace] >> dsimp[Once EXTENSION]
QED

Theorem X_DIFF_EQ_X:
  X DIFF Y = X â‡” DISJOINT X Y
Proof
  simp[DISJOINT_DEF, EXTENSION] >> metis_tac[]
QED

Theorem DIFF_EQ_EMPTY[simp]:
  X DIFF Y = âˆ… â‡” X âŠ† Y
Proof
  simp[EXTENSION, SUBSET_DEF] >> metis_tac[]
QED

Theorem closed_in_indiscrete[simp]:
  closed_in (indiscrete_topology X) s â‡” s = âˆ… âˆ¨ s = X
Proof
  simp[closed_in, EQ_IMP_THM, X_DIFF_EQ_X, DISJOINT_DEF] >> rpt strip_tac >>
  simp[] >- (gs[EXTENSION, SUBSET_DEF] >> metis_tac[]) >>
  metis_tac[SUBSET_ANTISYM]
QED

Theorem prop1_1_9:
  (âˆ€x. x âˆˆ topspace t â‡’ open_in t {x}) â‡’
  t = discrete_topology (topspace t)
Proof
  simp[TOPOLOGY_EQ, openSets_discrete] >> strip_tac >> gen_tac >> eq_tac
  >- (simp[topspace, SUBSET_DEF] >> metis_tac[]) >>
  gvs[topspace, PULL_EXISTS] >> simp[SUBSET_DEF] >> strip_tac >>
  â€˜s = BIGUNION {{x} | x âˆˆ s}â€™ by simp[Once EXTENSION, PULL_EXISTS] >>
  pop_assum SUBST1_TAC >> irule (cj 3 OPEN_IN_CLAUSES) >>
  simp[PULL_EXISTS]
QED

Theorem MAX_SET_all_x_lse_n_eq_n:
  MAX_SET {x | x â‰¤ n} = n
Proof
  DEEP_INTRO_TAC MAX_SET_ELIM >> rw[]
  >- (`{x | x â‰¤ n} = count (n+1)` suffices_by simp[] >> simp[EXTENSION])
  >- (fs[EXTENSION] >> fs[NOT_LESS_EQUAL] >> `n < 0` by fs[] >>
      `0 â‰¤ n` by fs[] >> fs[])
  >> metis_tac[LESS_EQ_REFL, LESS_EQUAL_ANTISYM]
QED

Theorem exercise1_1_6i:
  istopology ({univ (:num);âˆ…} UNION (IMAGE (\n. {x | x â‰¤ n}) univ(:num)))
Proof
  rw[istopology] >> simp[] >> TRY (metis_tac[]) (* 2 *)
  >- (disj2_tac >> qexists_tac â€˜MIN n n'â€™ >> rw[EQ_IMP_THM,EXTENSION]) >>
  Cases_on â€˜UNIV IN kâ€™ (* 2 *)
  >- (â€˜BIGUNION k = UNIVâ€™
        by (rw[EXTENSION,EQ_IMP_THM] >> metis_tac[IN_UNIV]) >>
      simp[]) >>
  Cases_on â€˜k = {âˆ…}â€™ >> simp[] >> Cases_on â€˜k = {}â€™ >> simp[] >>
  Cases_on â€˜FINITE kâ€™ (* 2 *)
  >- (fs[SUBSET_DEF] >> disj2_tac >>
      â€˜FINITE {n | {x | x â‰¤ n} âˆˆ k}â€™
        by (â€˜INJ (\n. {x | x â‰¤ n}) {n | {x | x â‰¤ n} âˆˆ k} (k DELETE {})â€™
              by (simp[INJ_DEF] >> rw[]
                  >- (simp[EXTENSION] >> qexists_tac â€˜0â€™ >> simp[])
                  >> pop_assum mp_tac >> simp[EXTENSION] >>
                  metis_tac[LESS_EQ_REFL, LESS_EQUAL_ANTISYM]) >>
            drule FINITE_INJ >> rw[]) >>
      qexists_tac â€˜MAX_SET {n | {x | x â‰¤ n} âˆˆ k}â€™ >> rw[EQ_IMP_THM,EXTENSION]
      >- (rename [â€˜n IN iâ€™,â€˜i IN kâ€™] >>
          â€˜âˆƒm. i = {x | x â‰¤ m}â€™ by metis_tac[NOT_IN_EMPTY] >>
          gvs[] >> DEEP_INTRO_TAC MAX_SET_ELIM >> simp[] >> rw[] (* 2 *)
          >- fs[EXTENSION]
          >> metis_tac[LESS_EQ_TRANS])
      >> pop_assum mp_tac >> DEEP_INTRO_TAC MAX_SET_ELIM >> rw[]
      >- (pop_assum mp_tac >> simp[EXTENSION] >> rw[] >>
          â€˜âˆƒy. {y' | y' â‰¤ y} âˆˆ kâ€™ suffices_by metis_tac[] >>
          â€˜âˆƒz. z âˆˆ k âˆ§ z â‰  âˆ…â€™ suffices_by metis_tac[] >>
          CCONTR_TAC >> gs[] >> â€˜âˆ€z. z âˆˆ k â‡’ z = âˆ…â€™ by metis_tac[] >>
          qpat_x_assum â€˜k â‰  {âˆ…}â€™ mp_tac >> simp[Once EXTENSION] >>
          rw[EQ_IMP_THM] >> metis_tac[MEMBER_NOT_EMPTY])
      >> first_x_assum $ irule_at Any >> simp[])
  >> fs[SUBSET_DEF] >> disj1_tac >> simp[EXTENSION] >> rw[] >>
  â€˜âˆƒy. {y' | y' â‰¤ y} âˆˆ k âˆ§ x â‰¤ yâ€™
    suffices_by (strip_tac >> first_x_assum $ irule_at Any >> simp[]) >>
  CCONTR_TAC >> gs[] >>
  â€˜âˆ€y. {y' | y' â‰¤ y} âˆˆ k â‡’ Â¬(x â‰¤ y)â€™ by metis_tac[] >>
  fs[NOT_LESS_EQUAL] >>
  qpat_x_assum â€˜INFINITE kâ€™ mp_tac >> simp[] >>
  â€˜FINITE (k DELETE {})â€™ suffices_by simp[] >>
  â€˜INJ (\s. MAX_SET s) (k DELETE {}) (count x)â€™
    by (simp[INJ_DEF] >> rw[]
        >- (first_x_assum drule >> rw[] >> fs[] >>
            first_x_assum drule >> rw[] >> DEEP_INTRO_TAC MAX_SET_ELIM >>
            simp[] >> â€˜{x | x â‰¤ n} = count (n+1)â€™ suffices_by simp[] >>
            simp[EXTENSION])
        >> â€˜âˆƒm n. s = {x | x â‰¤ m} âˆ§ s' = {x | x â‰¤ n}â€™ by metis_tac[] >>
        metis_tac[MAX_SET_all_x_lse_n_eq_n]) >>
  drule FINITE_INJ >> rw[]
QED

Definition final_segment_topology_def:
  final_segment_topology =
  topology ({âˆ…; ð•Œ(:num)} âˆª IMAGE (Î»n. { m | n â‰¤ m }) UNIV)
End

Theorem exercise1_1_6ii:
  istopology ({âˆ…; ð•Œ(:num)} âˆª IMAGE (Î»n. { m | n â‰¤ m }) UNIV)
Proof
  rw[istopology] >> simp[]
  >- metis_tac[]
  >- metis_tac[]
  >- (rename [â€˜{ m | a â‰¤ m } âˆ© { m | b â‰¤ m }â€™] >> rpt disj2_tac >>
      qexists_tac â€˜MAX a bâ€™ >> simp[EXTENSION])
  >- (gvs[SUBSET_INSERT', INSERT_UNION_EQ] >>
      rename [â€˜ss âŠ† IMAGE _ UNIVâ€™] >>
      Cases_on â€˜BIGUNION ss = âˆ…â€™ >> gvs[BIGUNION_EQ_EMPTY] >>
      â€˜BIGUNION ss = { m | MIN_SET (BIGUNION ss) â‰¤ m }â€™
        suffices_by metis_tac[] >>
      DEEP_INTRO_TAC MIN_SET_ELIM >> simp[PULL_EXISTS] >> rw[] >>
      simp[Once EXTENSION] >> gen_tac >>
      (rename [â€˜m âˆˆ sâ€™, â€˜s âˆˆ ssâ€™, â€˜m â‰¤ nâ€™] >> eq_tac >- metis_tac[] >>
       strip_tac >> qexists_tac â€˜sâ€™ >> simp[] >> gs[SUBSET_DEF] >>
       last_x_assum drule >> simp[EXTENSION, PULL_EXISTS] >> rw[] >>
       metis_tac[LESS_EQ_TRANS]))
QED

Theorem open_in_finalsegment[simp]:
  open_in final_segment_topology s â‡” s = âˆ… âˆ¨ âˆƒn. s = { m | n â‰¤ m }
Proof
  simp[topology_tybij |> cj 2 |> iffLR, final_segment_topology_def,
       exercise1_1_6ii] >> Cases_on â€˜s = âˆ…â€™ >> simp[] >>
  eq_tac >> simp[] >> strip_tac
  >- (qexists_tac â€˜0â€™ >> simp[]) >>
  metis_tac[]
QED


Theorem prop122i = CONJ OPEN_IN_TOPSPACE OPEN_IN_EMPTY
Theorem prop122ii = OPEN_IN_BIGUNION
Theorem prop122iii = OPEN_IN_BIGINTER

Theorem biginter_counterexample[local]:
  let S_n n = 1 INSERT { m | n + 1 â‰¤ m } (* S_n from Example 1_2_3 *)
  in
    (âˆ€n. FINITE (COMPL (S_n n))) âˆ§
    BIGINTER (IMAGE S_n UNIV) = {1}
Proof
  simp[] >> conj_tac
  >- (gen_tac >> irule SUBSET_FINITE >> qexists_tac â€˜count (n + 1)â€™ >>
      simp[SUBSET_DEF]) >>
  simp[Once EXTENSION, PULL_EXISTS] >> gen_tac >> eq_tac >> simp[] >>
  disch_then $ qspec_then â€˜xâ€™ mp_tac >> simp[]
QED

(* what the book calls a closed set is written closed_in, here we alias with
   closedSet *)
Overload closedSets = â€œclosed_inâ€

Theorem prop1_2_5:
  closed_in top âˆ… âˆ§ closed_in top (topspace top) âˆ§ (* i *)
  ((âˆ€t. t âˆˆ s â‡’ closed_in top t) âˆ§ s â‰  âˆ… â‡’ closed_in top (BIGINTER s)) âˆ§
  ((âˆ€t. t âˆˆ s â‡’ closed_in top t) âˆ§ FINITE s â‡’ closed_in top (BIGUNION s))
Proof
  simp[CLOSED_IN_BIGINTER, CLOSED_IN_BIGUNION, CLOSED_IN_EMPTY,
       CLOSED_IN_TOPSPACE]
QED

Definition clopen_def:
  clopen top s â‡” closed_in top s âˆ§ open_in top s
End

Theorem exercise1_2_2:
  (âˆ€s. s âŠ† topspace top â‡’ closed_in top s) â‡’
  top = discrete_topology (topspace top)
Proof
  strip_tac >> irule prop1_1_9 >>
  gs[closed_in] >> qx_gen_tac â€˜eâ€™ >> strip_tac >>
  first_x_assum $ qspec_then â€˜topspace top DIFF {e}â€™ mp_tac >> simp[] >>
  simp[DIFF_DIFF_SUBSET, SUBSET_DEF]
QED

Theorem exercise1_2_3:
  ALL_DISTINCT [a;b;c;d] â‡’
  let t = {âˆ…; {a;c}; {b;d}; {a;b;c;d}}
  in
  istopology t âˆ§ (âˆ€s. open_in (topology t) s â‡’ closed_in (topology t) s)
Proof
  simp[] >> strip_tac >> conj_asm1_tac
  >- (simp[istopology] >> rw[] >> simp[] >>
      simp[SING_INTER, INTER_EQ] >>
      simp[Once EXTENSION]
      >- metis_tac[] >- metis_tac[] >>
      gs[SUBSET_DEF] >> Cases_on â€˜k = âˆ…â€™ >> simp[] >>
      ONCE_REWRITE_TAC [EXTENSION] >>
      Cases_on â€˜k = {âˆ…}â€™ (* 2 *)
      >- simp[] >>
      disj2_tac >> Cases_on â€˜{a;b;c;d} IN kâ€™ (* 2 *)
      >- (disj2_tac >> disj2_tac >>
          simp[EQ_IMP_THM,PULL_EXISTS,FORALL_AND_THM,DISJ_IMP_THM] >>
          ntac 4 (qexists_tac â€˜{a;b;c;d}â€™) >> simp[] >>
          rpt strip_tac >> first_x_assum (drule_then strip_assume_tac) (* 4 *)>>
          fs[]) >>
      Cases_on â€˜{a;c} IN kâ€™ >> Cases_on â€˜{b;d} IN kâ€™ (* 4 *)
      >- (disj2_tac >> disj2_tac >>
         simp[EQ_IMP_THM,PULL_EXISTS,FORALL_AND_THM,DISJ_IMP_THM] >>
         qexistsl_tac [â€˜{a;c}â€™,â€˜{b;d}â€™,â€˜{a;c}â€™,â€˜{b;d}â€™] >> simp[] >>
         rpt strip_tac >> first_x_assum (drule_then strip_assume_tac) (* 4 *) >>
         fs[])
      >- (disj1_tac >>
         simp[EQ_IMP_THM,PULL_EXISTS,FORALL_AND_THM,DISJ_IMP_THM] >>
         ntac 2 (qexists_tac â€˜{a;c}â€™) >> simp[] >>
         rpt strip_tac >> first_x_assum (drule_then strip_assume_tac) (* 4 *) >>
         fs[])
      >- (disj2_tac >> disj1_tac >>
         simp[EQ_IMP_THM,PULL_EXISTS,FORALL_AND_THM,DISJ_IMP_THM] >>
         ntac 2 (qexists_tac â€˜{b;d}â€™) >> simp[] >>
         rpt strip_tac >> first_x_assum (drule_then strip_assume_tac) (* 4 *) >>
         fs[]) >>
      â€˜âˆ€x. x âˆˆ k â‡’ x = âˆ…â€™ by metis_tac[] >>
      â€˜k = {âˆ…}â€™ suffices_by metis_tac[] >>
      rw[EXTENSION] >> fs[GSYM MEMBER_NOT_EMPTY] >>
      metis_tac[UNIQUE_MEMBER_SING])
  >> `topspace (topology {âˆ…; {a; c}; {b; d}; {a; b; c; d}}) = {a; b; c; d}`
        by (simp[topspace, topology_tybij |> cj 2 |> iffLR] >>
            simp[Once EXTENSION] >> dsimp[] >> metis_tac[]) >>
  simp[topology_tybij |> cj 2 |> iffLR, closed_in] >>
  rw[] >> simp[] >> fs[] >> rw[]
QED

Definition finite_closed_topology_def:
  finite_closed_topology X =
  topology (âˆ… INSERT { s | FINITE (X DIFF s) âˆ§ s âŠ† X})
End

Theorem finite_closed_topology_istopology:
  istopology (âˆ… INSERT { s | FINITE (X DIFF s) âˆ§ s âŠ† X})
Proof
  rw[istopology] >> simp[]
  >- (disj2_tac >> conj_tac
      >- (â€˜X DIFF s âˆ© t = (X DIFF s) âˆª (X DIFF t)â€™
            by (simp[EXTENSION] >> metis_tac[]) >>
          simp[]) >> gs[SUBSET_DEF]) >>
  Cases_on â€˜k = âˆ…â€™ >> fs[] >> Cases_on â€˜k = {âˆ…}â€™ >> fs[] >>
  â€˜X DIFF (BIGUNION k) = BIGINTER { X DIFF t | t âˆˆ k }â€™
    by (simp[Once EXTENSION, PULL_EXISTS] >> metis_tac[MEMBER_NOT_EMPTY]) >>
  conj_tac
  >- (simp[] >> irule FINITE_BIGINTER >> simp[PULL_EXISTS] >>
      â€˜âˆƒt. t âˆˆ k âˆ§ t â‰  âˆ…â€™
        by (qpat_x_assum â€˜k â‰  âˆ…â€™ mp_tac >> qpat_x_assum â€˜k â‰  {âˆ…}â€™ mp_tac >>
            ONCE_REWRITE_TAC[EXTENSION] >> simp[] >>
            metis_tac[MEMBER_NOT_EMPTY]) >>
      qexists_tac â€˜tâ€™ >> rw[] >> gvs[SUBSET_DEF] >> metis_tac[]) >>
  simp[BIGUNION_SUBSET] >> rw[] >> qpat_x_assum â€˜k âŠ† _â€™ mp_tac >>
  simp[SimpL â€œ$==>â€, Once SUBSET_DEF] >>
  rw[] >> first_x_assum drule >> rw[] >> simp[]
QED

Theorem finite_closed_open_sets[simp]:
  open_in (finite_closed_topology X) s â‡” s = âˆ… âˆ¨ FINITE (X DIFF s) âˆ§ s âŠ† X
Proof
  simp[topology_tybij |> cj 2 |> iffLR, finite_closed_topology_def,
       finite_closed_topology_istopology]
QED

Theorem finite_closed_topspace[simp]:
  topspace (finite_closed_topology X) = X
Proof
  simp[topspace, Once EXTENSION, EQ_IMP_THM] >> rw[] (* 3 *)
  >- gvs[]
  >- gvs[SUBSET_DEF]
  >- (first_assum (irule_at (Pos hd)) >> simp[])
QED

Theorem finite_closed_closed_sets[simp]:
  closed_in (finite_closed_topology X) s â‡” s = X âˆ¨ s âŠ† X âˆ§ FINITE s
Proof
  simp[closed_in] >> csimp[DIFF_DIFF_SUBSET, SUBSET_DIFF_EMPTY] >>
  metis_tac[SUBSET_ANTISYM, SUBSET_REFL]
QED

val _ = export_rewrites ["topology.CLOSED_IN_EMPTY",
                         "topology.OPEN_IN_EMPTY",
                         "topology.OPEN_IN_TOPSPACE",
                         "topology.CLOSED_IN_TOPSPACE"]

Theorem clopen_EMPTY[simp]:
  clopen top {}
Proof
  simp[clopen_def]
QED

Theorem clopen_topspace[simp]:
  clopen top (topspace top)
Proof
  simp[clopen_def]
QED

Theorem example1_3_3:
  (âˆƒa b c. clopen (finite_closed_topology X) a âˆ§
           clopen (finite_closed_topology X) b âˆ§
           clopen (finite_closed_topology X) c âˆ§ a â‰  b âˆ§ b â‰  c âˆ§ a â‰  c) â‡’
  FINITE X
Proof
  strip_tac >>
  â€˜âˆƒd. clopen (finite_closed_topology X) d âˆ§ d â‰  âˆ… âˆ§ d â‰  Xâ€™
    by metis_tac[clopen_EMPTY, clopen_topspace, finite_closed_topspace] >>
  qpat_x_assum â€˜clopen _ _â€™ mp_tac >>
  csimp[clopen_def] >> strip_tac >>
  â€˜X = d âˆª (X DIFF d)â€™ by simp[UNION_DIFF] >>
  metis_tac[FINITE_UNION]
QED

(* topologies are preserved over preimages *)
Theorem example1_3_9:
  istopology {PREIMAGE f s | open_in (yt : 'b topology) s}
Proof
  simp[istopology] >> rw[] (* 3 *)
  >- (irule_at (Pos hd) PREIMAGE_EMPTY >> simp[])
  >- (rename [â€˜PREIMAGE f s1 âˆ© PREIMAGE f s2â€™] >>
      irule_at (Pos hd) (GSYM PREIMAGE_INTER) >>
      simp[OPEN_IN_INTER])
  >- (gvs[SUBSET_DEF, GSYM RIGHT_EXISTS_IMP_THM, SKOLEM_THM] >>
      rename [â€˜_ = PREIMAGE f (g _) âˆ§ open_in yt (g _)â€™] >>
      qexists_tac â€˜BIGUNION (IMAGE g k)â€™ >> conj_tac
      >- (simp[Once EXTENSION, PULL_EXISTS] >> qx_gen_tac â€˜eâ€™ >>
          eq_tac >> strip_tac (* 2 *)
          >- (first_assum (irule_at Any) >> first_x_assum drule >>
              simp[Once EXTENSION] >> metis_tac[]) >>
          first_x_assum drule >> simp[Once EXTENSION] >> metis_tac[]) >>
      irule OPEN_IN_BIGUNION >> simp[PULL_EXISTS])
QED

Definition sierpinski_space_def:
  sierpinski_space x y = topology {âˆ…; {x}; {x; y}}
End

Theorem sierpinski_space_is_topology:
  istopology ({âˆ…; {x}; {x; y}})
Proof
  rw[istopology] >> simp[]
  >- (simp[EXTENSION] >> metis_tac[])
  >- (simp[EXTENSION] >> metis_tac[]) >>
  â€˜k âˆˆ POW {âˆ…; {x}; {x; y}}â€™ by simp[IN_POW] >>
  pop_assum mp_tac >> pop_assum (K ALL_TAC) >>
  simp[pow3] >> rw[] >> ONCE_REWRITE_TAC[EXTENSION] >> dsimp[] >> metis_tac[]
QED

Theorem open_in_sierpinski_space[simp]:
 open_in (sierpinski_space x y) s â‡” s = {} âˆ¨ s = {x} âˆ¨ s = {x; y}
Proof
simp[sierpinski_space_def,cj 2 topology_tybij |> iffLR,
     sierpinski_space_is_topology]
QED

Theorem topspace_sierpinski_space[simp]:
 topspace (sierpinski_space x y) = {x;y}
Proof
simp[topspace,Once EXTENSION] >> dsimp[] >> metis_tac[]
QED

Definition T1_space_def:
  T1_space X â‡” âˆ€x. x IN topspace X â‡’ closed_in X {x}
End

Theorem topspace_discrete_topology[simp]:
âˆ€X. topspace (discrete_topology X) = X
Proof
simp[topspace,discrete_topology_def,EXTENSION,PULL_EXISTS] >>
metis_tac[SUBSET_DEF]
QED


Theorem closed_in_discrete_topology[simp]:
âˆ€X s. closed_in (discrete_topology X) s â‡” s âŠ† X
Proof
simp[closed_in]
QED


Theorem discrete_topology_T1:
âˆ€X. T1_space (discrete_topology X)
Proof
simp[T1_space_def]
QED

Theorem finite_closed_topology_T1:
âˆ€X. T1_space (finite_closed_topology X)
Proof
simp[T1_space_def]
QED


Definition T0_space_def:
  T0_space X â‡” âˆ€a b. a IN topspace X âˆ§ b IN topspace X âˆ§ a â‰  b â‡’
                     âˆƒs. open_in X s âˆ§ (a IN s âˆ§ b NOTIN s âˆ¨ b IN s âˆ§ a NOTIN s)
End

Theorem T1_is_T0:
  âˆ€X. T1_space X â‡’ T0_space X
Proof
  simp[T1_space_def,T0_space_def,closed_in] >> rw[] >>
  qexists_tac â€˜(topspace X) DIFF {a}â€™ >> simp[]
QED

Theorem sierpinski_space_T0:
  T0_space (sierpinski_space x y)
Proof
  simp[T0_space_def] >> rw[] (* 2 *)
  >- (qexists_tac â€˜{a}â€™ >> simp[]) >- (qexists_tac â€˜{b}â€™ >> simp[])
QED

Definition countable_closed_topology_def:
  countable_closed_topology X =
  topology (âˆ… INSERT { s | countable (X DIFF s) âˆ§ s âŠ† X})
End

Theorem countable_BIGINTER:
  (âˆƒs. s âˆˆ P âˆ§ countable s) â‡’ countable (BIGINTER P)
Proof
  strip_tac >> irule COUNTABLE_SUBSET >> qexists_tac `s` >> rw[] >>
  irule BIGINTER_SUBSET >> metis_tac[SUBSET_REFL]
QED

Theorem countable_closed_topology_istopology:
  istopology (âˆ… INSERT { s | countable (X DIFF s) âˆ§ s âŠ† X})
Proof
  rw[istopology] >> simp[]
  >- (disj2_tac >> conj_tac
      >- (â€˜X DIFF s âˆ© t = (X DIFF s) âˆª (X DIFF t)â€™
            by (simp[EXTENSION] >> metis_tac[]) >>
          simp[])
      >> gs[SUBSET_DEF]) >>
  Cases_on â€˜k = âˆ…â€™ >> fs[] >> Cases_on â€˜k = {âˆ…}â€™ >> fs[] >>
  â€˜X DIFF (BIGUNION k) = BIGINTER { X DIFF t | t âˆˆ k }â€™
    by (simp[Once EXTENSION, PULL_EXISTS] >> metis_tac[MEMBER_NOT_EMPTY]) >>
  conj_tac
  >- (simp[] >> irule countable_BIGINTER >> simp[PULL_EXISTS] >>
      â€˜âˆƒt. t âˆˆ k âˆ§ t â‰  âˆ…â€™
        by (qpat_x_assum â€˜k â‰  âˆ…â€™ mp_tac >> qpat_x_assum â€˜k â‰  {âˆ…}â€™ mp_tac >>
            ONCE_REWRITE_TAC[EXTENSION] >> simp[] >>
            metis_tac[MEMBER_NOT_EMPTY]) >>
      qexists_tac â€˜tâ€™ >> rw[] >> gvs[SUBSET_DEF] >> metis_tac[]) >>
  simp[BIGUNION_SUBSET] >> rw[] >> qpat_x_assum â€˜k âŠ† _â€™ mp_tac >>
  simp[SimpL â€œ$==>â€, Once SUBSET_DEF] >>
  rw[] >> first_x_assum drule >> rw[] >> simp[]
QED

Theorem countable_closed_open_sets[simp]:
  open_in (countable_closed_topology X) s â‡” s = âˆ… âˆ¨ countable (X DIFF s) âˆ§ s âŠ† X
Proof
  simp[topology_tybij |> cj 2 |> iffLR, countable_closed_topology_def,
       countable_closed_topology_istopology]
QED

Theorem topspace_countable_closed[simp]:
  topspace (countable_closed_topology X) = X
Proof
  simp[topspace, Once EXTENSION, EQ_IMP_THM] >> rw[] (* 3 *)
  >- gvs[]
  >- gvs[SUBSET_DEF]
  >- (first_assum (irule_at (Pos hd)) >> simp[])
QED

Definition top_INTER_def:
  top_INTER t1 t2 = topology {s | open_in t1 s âˆ§ open_in t2 s}
End

Theorem top_INTER_istopology:
  istopology {s | open_in t1 s âˆ§ open_in t2 s}
Proof
  rw[istopology] >> simp[OPEN_IN_INTER] >>
  irule OPEN_IN_BIGUNION >> rw[] >> fs[SUBSET_DEF]
QED

Theorem top_INTER_open_sets[simp]:
  open_in (top_INTER t1 t2) s â‡” open_in t1 s âˆ§ open_in t2 s
Proof
  simp[topology_tybij |> cj 2 |> iffLR, top_INTER_def,
       top_INTER_istopology]
QED

(*
Thm topspace_top_INTER_old:
topspace (top_INTER t1 t2) = topspace t1 âˆ© topspace t2

topspace_top_INTER_old is false.
Counter example:
t1 = empty, {a;b}
t2 = empty, {b;c}
*)

Theorem topspace_top_INTER[simp]:
  topspace t1 = topspace t2 â‡’ topspace (top_INTER t1 t2) = topspace t2
Proof
  simp[topspace] >> ONCE_REWRITE_TAC [EXTENSION] >> rw[] >> eq_tac
  >- metis_tac[]
  >> rw[] >> qexists_tac `topspace t1` >> rw[]
  >- (rw[topspace] >> metis_tac[])
  >> `topspace t1 = topspace t2` suffices_by simp[] >> rw[topspace, Once EXTENSION]
QED

Theorem exercise1_3_7_iii:
  topspace t1 = topspace t2 âˆ§ T1_space t1 âˆ§ T1_space t2 â‡’
  T1_space (top_INTER t1 t2)
Proof
  simp[T1_space_def] >> rw[] >> fs[closed_in] >> metis_tac[]
QED

Definition top_BIGINTER_def:
  top_BIGINTER X = topology (BIGINTER (IMAGE open_in X))
End

Theorem IN_open_in[simp]:
  s âˆˆ open_in t â‡” open_in t s
Proof
  simp[IN_DEF]
QED

Theorem exercise1_3_7_vi[simp]:
  open_in (top_BIGINTER X) s <=> âˆ€t. t âˆˆ X â‡’ open_in t s
Proof
  simp[top_BIGINTER_def] >>
  â€˜istopology (BIGINTER (IMAGE open_in X))â€™
    by (simp[istopology, PULL_EXISTS, OPEN_IN_INTER] >> rpt strip_tac >>
        irule OPEN_IN_BIGUNION >> gs[SUBSET_DEF, PULL_EXISTS]) >>
  drule (topology_tybij |> cj 2 |> iffLR) >> simp[PULL_EXISTS]
QED

Theorem topspace_top_BIGINTER:
  Ts â‰  âˆ… âˆ§ (âˆ€t. t âˆˆ Ts â‡’ topspace t = X) â‡’ topspace (top_BIGINTER Ts) = X
Proof
  strip_tac >>
  simp[topspace] >> ONCE_REWRITE_TAC[EXTENSION] >> simp[] >>
  rpt strip_tac >> eq_tac >> rpt strip_tac
  >- (fs[topspace,Once EXTENSION] >> metis_tac[]) >>
  rename [â€˜_ IN Ts â‡’ _â€™, â€˜Ts â‰  âˆ…â€™, â€˜y IN _ âˆ§ _â€™] >>
  qexists_tac â€˜Xâ€™ >> simp[] >> rpt strip_tac >> first_x_assum drule >>
  strip_tac >> gvs[OPEN_IN_TOPSPACE]
QED

Definition door_space_def:
  door_space t â‡” âˆ€s. s âŠ† topspace t â‡’ open_in t s âˆ¨ closed_in t s
End

Theorem exercise1_3_9_i:
  door_space (discrete_topology X)
Proof
  simp[door_space_def]
QED

Theorem exercise1_3_9_ii:
  a â‰  b â‡’ Â¬door_space (indiscrete_topology {a;b})
Proof
  simp[door_space_def] >> csimp[] >> strip_tac >> qexists_tac â€˜{a}â€™ >>
  simp[EXTENSION] >> metis_tac[]
QED

Theorem finite_open:
  INFINITE X â‡’ ~istopology { s | FINITE s âˆ§ s âŠ† X }
Proof
  simp[infinite_num_inj, INJ_IFF] >>
  rw[istopology] >> disj2_tac >>
  qexists_tac â€˜IMAGE (Î»n. {f n}) UNIVâ€™ >> simp[] >>
  simp[SUBSET_DEF, PULL_EXISTS]
QED

Theorem exercise1_3_9_iii:
  INFINITE X â‡’ Â¬door_space (finite_closed_topology X)
Proof
  simp[door_space_def] >> simp[SimpL â€œ$==>â€, infinite_num_inj, INJ_IFF] >>
  strip_tac >> qexists_tac â€˜{ f n | EVEN n}â€™  >>
  simp[SUBSET_DEF, PULL_EXISTS] >> rpt strip_tac
  >- (gs[EXTENSION] >> metis_tac[EVEN])
  >- (drule_then (qspec_then â€˜{f n | ODD n}â€™ mp_tac) SUBSET_FINITE >>
      simp[SUBSET_DEF, PULL_EXISTS, ODD_EVEN] >>
      simp[infinite_num_inj] >> qexists_tac â€˜Î»n. f (2 * n + 1)â€™ >>
      simp[INJ_IFF, EVEN_ADD, EVEN_MULT])
  >- (pop_assum mp_tac >> simp[EXTENSION] >> qexists_tac â€˜f (SUC 0)â€™ >> simp[]) >>
  pop_assum mp_tac >> simp[infinite_num_inj] >> qexists_tac â€˜f o $* 2â€™ >>
  simp[INJ_IFF, EVEN_MULT]
QED

Definition saturated_def:
  saturated t s â‡” s âŠ† topspace t âˆ§
                  âˆƒos. os â‰  âˆ… âˆ§ s = BIGINTER os âˆ§ (âˆ€x. x âˆˆ os â‡’ open_in t x)
End

Theorem exercise1_3_10_i:
  open_in t s â‡’ saturated t s
Proof
  simp[saturated_def, OPEN_IN_SUBSET] >> strip_tac >>
  qexists_tac â€˜{s}â€™ >> simp[]
QED

Theorem exercise1_3_10_ii:
  T1_space t â‡’ âˆ€s. s âŠ† topspace t â‡’ saturated t s
Proof
  simp[T1_space_def, saturated_def] >> rpt strip_tac >>
  Cases_on â€˜open_in t sâ€™
  >- (qexists_tac â€˜{s}â€™ >> simp[]) >>
  qabbrev_tac â€˜sbar = topspace t DIFF sâ€™ >>
  â€˜sbar âŠ† topspace tâ€™ by simp[Abbrâ€˜sbarâ€™] >>
  qabbrev_tac â€˜OS = { topspace t DIFF {e} | e âˆˆ sbar }â€™ >>
  â€˜OS â‰  âˆ…â€™
    by (simp[Once EXTENSION, Abbrâ€˜OSâ€™, Abbrâ€˜sbarâ€™] >>
        â€˜topspace t DIFF s â‰  âˆ…â€™ by
          (simp[] >> strip_tac >> metis_tac[SUBSET_ANTISYM, OPEN_IN_TOPSPACE]) >>
        drule_then (qx_choose_then â€˜eeâ€™ mp_tac) (iffRL MEMBER_NOT_EMPTY) >>
        simp[] >> metis_tac[]) >>
  â€˜s = BIGINTER OSâ€™
    by (simp[Once EXTENSION, Abbrâ€˜sbarâ€™, PULL_EXISTS, Abbrâ€˜OSâ€™] >>
        qx_gen_tac â€˜aâ€™ >>
        eq_tac >> rpt strip_tac
        >- gs[SUBSET_DEF]
        >- metis_tac[]
        >- (â€˜a âˆˆ topspace tâ€™ suffices_by metis_tac[] >>
            â€˜topspace t DIFF s â‰  âˆ…â€™ by
              (simp[] >> strip_tac >> metis_tac[SUBSET_ANTISYM, OPEN_IN_TOPSPACE]) >>
            drule_then (qx_choose_then â€˜eeâ€™ mp_tac) (iffRL MEMBER_NOT_EMPTY) >>
            simp[] >> metis_tac[])) >>
  qexists_tac â€˜OSâ€™ >> simp[] >> simp[Abbrâ€˜OSâ€™, PULL_EXISTS, Abbrâ€˜sbarâ€™] >>
  metis_tac[closed_in]
QED

Theorem exercise1_3_10_iii:
  a â‰  b â‡’ Â¬saturated (indiscrete_topology {a;b}) {a}
Proof
  simp[saturated_def] >> rpt strip_tac >> Cases_on â€˜os = âˆ…â€™ >> gs[]>>
  CCONTR_TAC >> gs[] >> qpat_x_assum â€˜{a} = _â€™ mp_tac >>
  simp[EXTENSION] >>
  â€˜âˆ€s. s âˆˆ os â‡’ s = {a;b} âˆ¨ s = âˆ…â€™ by metis_tac[] >>
  Cases_on â€˜os = {{a;b}}â€™
  >- (qexists_tac â€˜bâ€™ >> simp[]) >>
  â€˜âˆ… âˆˆ osâ€™ by (pop_assum mp_tac >> simp[Once EXTENSION, PULL_EXISTS] >>
               qx_gen_tacâ€˜sâ€™ >> strip_tac >>
               â€˜s âˆˆ os â‡” s â‰  {a;b}â€™ by metis_tac[] >>
               Cases_on â€˜s = {a;b}â€™ >> gs[]
               >- metis_tac[MEMBER_NOT_EMPTY] >> metis_tac[]) >>
  qexists_tac â€˜aâ€™ >> simp[] >> first_assum (irule_at Any) >> simp[]
QED

Theorem exercise1_3_10_iv:
  (âˆ€s. s âŠ† topspace t â‡’ saturated t s) â‡’ T1_space t
Proof
  simp[saturated_def, T1_space_def] >> strip_tac >>
  qx_gen_tac â€˜aâ€™ >> strip_tac >>
  first_x_assum $ qspec_then â€˜topspace t DIFF {a}â€™ mp_tac >> simp[] >>
  disch_then $ qx_choose_then â€˜ssâ€™ strip_assume_tac >>
  simp[closed_in] >> first_assum irule >>
  â€˜âˆ€s. s âˆˆ ss â‡’ BIGINTER ss âŠ† sâ€™
    by metis_tac[BIGINTER_SUBSET, SUBSET_REFL] >>
  â€˜âˆ€s. s âˆˆ ss â‡’ s âŠ† topspace tâ€™ by metis_tac[OPEN_IN_SUBSET] >>
  â€˜âˆƒs. s âˆˆ ss âˆ§ a âˆ‰ sâ€™
    by (CCONTR_TAC >> gs[] >>
        â€˜a IN BIGINTER ssâ€™ by (simp[] >> metis_tac[]) >>
        qpat_x_assum â€˜_ = BIGINTER ssâ€™ (SUBST_ALL_TAC o SYM) >> gs[]) >>
  â€˜BIGINTER ss = sâ€™ suffices_by simp[] >>
  irule SUBSET_ANTISYM >> conj_tac >- metis_tac[] >>
  â€˜s âŠ† topspace t DIFF {a}â€™ suffices_by simp[] >>
  gs[SUBSET_DEF] >> metis_tac[]
QED


val _ = export_theory();
