open HolKernel Parse boolLib bossLib;
open topologyTheory pred_setTheory
open chap1Theory chap2_2Theory chap3Theory;

val _ = new_theory "chap4";

(* ===============================
      Chapter 4. Homeomorphisms
   =============================== *)

(* subspace topology is already defined in HOL
   as subtopology *)

Theorem example_4_1_4:
	basis B t â‡’
	basis {b âˆ© Y | b âˆˆ B} (subtopology t Y)
Proof
	rw[basis_def, OPEN_IN_SUBTOPOLOGY]
	>- metis_tac[]
	>> first_x_assum drule >> rw[] >>
	qexists_tac `{b âˆ© Y | b âˆˆ u}` >> rw[]
	>- (rw[SUBSET_DEF] >> metis_tac[SUBSET_DEF])
	>> rw[Once EXTENSION] >> simp[PULL_EXISTS] >>
	metis_tac[]
QED

Theorem exercise_4_1_4:
  A âŠ† B (* âˆ§ B âŠ† topspace Ï„ *) â‡’
  subtopology Ï„ A = subtopology (subtopology Ï„ B) A
Proof
  rw[TOPOLOGY_EQ, OPEN_IN_SUBTOPOLOGY, PULL_EXISTS] >>
  â€˜B âˆ© A = Aâ€™ by gs[EXTENSION, SUBSET_DEF, SF CONJ_ss] >>
  ASM_REWRITE_TAC [GSYM INTER_ASSOC]
QED

Theorem exercise_4_1_5:
  closed_in (subtopology Ï„ Y) Z â‡” âˆƒA. Z = A âˆ© Y âˆ§ closed_in Ï„ A
Proof
  rw[OPEN_IN_SUBTOPOLOGY, closed_in, TOPSPACE_SUBTOPOLOGY, EQ_IMP_THM] (* 4 *) >~
  [â€˜topspace Ï„ âˆ© Y DIFF Z = A âˆ© Yâ€™]
  >- (qexists_tac â€˜topspace Ï„ DIFF Aâ€™ >>
      simp[DIFF_DIFF_SUBSET, OPEN_IN_SUBSET] >>
      simp[DIFF_INTER] >> gs[EXTENSION] >> metis_tac[SUBSET_DEF]) >~
  [â€˜A âˆ© Y âŠ† topspace Ï„â€™]
  >- metis_tac[SUBSET_TRANS, INTER_SUBSET] >~
  [â€˜A âˆ© Y âŠ† Yâ€™] >- simp[INTER_SUBSET] >>
  rename[â€˜open_in Ï„ (topspace Ï„ DIFF A)â€™] >>
  first_assum $ irule_at (Pat â€˜open_in _ _â€™) >>
  gs[EXTENSION, SUBSET_DEF] >> metis_tac[]
QED

Theorem exercise_4_1_6[simp]:
  subtopology (discrete_topology X) Y = discrete_topology (X âˆ© Y)
Proof
  simp[TOPOLOGY_EQ, OPEN_IN_SUBTOPOLOGY, EQ_IMP_THM] >> rpt strip_tac >>
  gvs[] (* 2 *) >>
  metis_tac[SUBSET_DEF, IN_INTER, EXTENSION]
QED

Theorem exercise_4_1_7[simp]:
  subtopology (indiscrete_topology X) Y = indiscrete_topology (X âˆ© Y)
Proof
  simp[TOPOLOGY_EQ, OPEN_IN_SUBTOPOLOGY, EQ_IMP_THM] >> rpt strip_tac >>
  gvs[] >> simp[SF DNF_ss]
QED

(*hint: Y is open in subtopology Ï„ Y*)
Theorem exercise_4_1_10:
  Y âŠ† topspace Ï„ â‡’
  ({s | open_in (subtopology Ï„ Y) s} âŠ† {s | open_in Ï„ s} â‡” open_in Ï„ Y)
Proof
  strip_tac >>
  rw[SUBSET_DEF,OPEN_IN_SUBTOPOLOGY,PULL_EXISTS,EQ_IMP_THM,OPEN_IN_INTER] >>
  first_x_assum $ qspec_then â€˜topspace Ï„â€™ assume_tac >>
  gs[SUBSET_INTER2]
QED

Theorem exercise_4_1_11:
  A âŠ† topspace Ï„ âˆ§ B âŠ† topspace Ï„ âˆ§ connected (subtopology Ï„ A) âˆ§
  connected (subtopology Ï„ B) âˆ§ A âˆ© B â‰  âˆ… â‡’ connected (subtopology Ï„ (A âˆª B))
Proof
  rw[connected_def,TOPSPACE_SUBTOPOLOGY,clopen_def,OPEN_IN_SUBTOPOLOGY,
     CLOSED_IN_SUBTOPOLOGY] >> eq_tac >> strip_tac (* 3 *)
  >- (gvs[] >> rename [â€˜open_in Ï„ Xâ€™,â€˜closedSets Ï„ Yâ€™] >>
      â€˜X âˆ© A = Y âˆ© A âˆ§ X âˆ© B = Y âˆ© Bâ€™ by
        (qpat_x_assum â€˜X âˆ© (A âˆª B) = Y âˆ© (A âˆª B)â€™ mp_tac >> simp[EXTENSION] >>
         metis_tac[]) >>
      â€˜(X âˆ© A = topspace Ï„ âˆ© A âˆ¨ X âˆ© A = {}) âˆ§
       (X âˆ© B = topspace Ï„ âˆ© B âˆ¨ X âˆ© B = {})â€™
        by metis_tac[] >>
      gs[SUBSET_DEF,EXTENSION] >> metis_tac[])
  >- metis_tac[CLOSED_IN_TOPSPACE,OPEN_IN_TOPSPACE] >>
  metis_tac[OPEN_IN_EMPTY,CLOSED_IN_EMPTY,INTER_EMPTY]
QED


Theorem excercise_4_1_12:
  T1_space Ï„ â‡’ T1_space (subtopology Ï„ Y)
Proof
  fs[T1_space_def,CLOSED_IN_SUBTOPOLOGY,TOPSPACE_SUBTOPOLOGY] >>
  rw[] >> ntac 2 (first_assum (irule_at Any)) >>
  simp[EXTENSION] >> metis_tac[]
QED


Definition T2_space_def:
  T2_space Ï„ â‡” âˆ€a b. a âˆˆ topspace Ï„ âˆ§ b âˆˆ topspace Ï„ âˆ§ a â‰  b â‡’
               âˆƒA B. open_in Ï„ A âˆ§ open_in Ï„ B âˆ§ a âˆˆ A âˆ§ b âˆˆ B âˆ§ A âˆ© B = âˆ…
End

Overload Hausdorff[inferior] = â€œT2_spaceâ€

Theorem excercise_4_1_13_ii:
  T2_space (discrete_topology X)
Proof
  rw[T2_space_def] >>
  rename1 â€˜a â‰  bâ€™ >>
  qexistsl_tac [â€˜{a}â€™,â€˜{b}â€™] >>
  rw[EXTENSION]
QED

Theorem excercise_4_1_13_iii:
  T2_space Ï„ â‡’ T1_space Ï„
Proof
  rw[T2_space_def,T1_space_def,closed_in] >>
  â€˜âˆƒAS. topspace Ï„ DIFF {x} = BIGUNION AS âˆ§ âˆ€A. A âˆˆ AS â‡’ open_in Ï„ Aâ€™
    suffices_by simp[PULL_EXISTS,OPEN_IN_BIGUNION] >>
  â€˜âˆ€y. y â‰  x âˆ§ y âˆˆ topspace Ï„ â‡’ âˆƒA. y âˆˆ A âˆ§ x âˆ‰ A âˆ§ open_in Ï„ Aâ€™
    suffices_by
      (rw[] >>
       pop_assum (strip_assume_tac o
                  SIMP_RULE bool_ss [GSYM RIGHT_EXISTS_IMP_THM,SKOLEM_THM]) >>
       qexists_tac â€˜{f y | y âˆˆ topspace Ï„ âˆ§ y â‰  x}â€™ >> simp[Once EXTENSION] >>
       rpt strip_tac >> rw[PULL_EXISTS,EQ_IMP_THM] >>
       metis_tac[OPEN_IN_SUBSET,SUBSET_DEF]) >>
  rw[] >> first_x_assum drule_all >> rw[] >> gs[EXTENSION] >>
  metis_tac[]
QED

Theorem excercise_4_1_13_iv_a:
  T1_space (finite_closed_topology ð•Œ(:num))
Proof
  simp[T1_space_def]
QED

Theorem excercise_4_1_13_iv_b:
  Â¬T2_space (finite_closed_topology ð•Œ(:num))
Proof
  simp[T2_space_def] >> qexistsl_tac [â€˜1â€™, â€˜2â€™] >> simp[] >>
  rpt strip_tac >> Cases_on â€˜1 âˆˆ Aâ€™ >> simp[] >>
  Cases_on â€˜2 âˆˆ Bâ€™ >> simp[] >>
  simp[GSYM MEMBER_NOT_EMPTY, SF SFY_ss] >> CCONTR_TAC >>
  gs[] >> qabbrev_tac â€˜A' = UNIV DIFF Aâ€™ >>
  qabbrev_tac â€˜B' = UNIV DIFF Bâ€™ >>
  â€˜FINITE (A' UNION B')â€™ by simp[] >>
  â€˜A' âˆª B' = UNIVâ€™ by
    (simp[EXTENSION, Abbr â€˜A'â€™, Abbr â€˜B'â€™] >>
     gs[EXTENSION]) >>
  pop_assum SUBST_ALL_TAC >>  gs[]
QED

Theorem exercise_4_1_13_v:
  T2_space Ï„ â‡’ T2_space (subtopology Ï„ X)
Proof
  simp[T2_space_def, TOPSPACE_SUBTOPOLOGY, OPEN_IN_SUBTOPOLOGY] >>
  rpt strip_tac >> simp[PULL_EXISTS] >>
  first_x_assum (drule_all_then strip_assume_tac) >>
  first_assum $ irule_at (Pat â€˜_ âˆˆ _â€™) >>
  first_assum $ irule_at (Pat â€˜_ âˆˆ _â€™) >> simp[] >>
  gs[EXTENSION] >> metis_tac[]
QED

Theorem exercise_4_1_13_vi:
  T2_space Ï„ âˆ§ door_space Ï„ â‡’
  (âˆ€x y. limpt Ï„ x (topspace Ï„) âˆ§ limpt Ï„ y (topspace Ï„) â‡’ x = y) âˆ§
  (âˆ€z. z âˆˆ topspace Ï„ âˆ§ Â¬limpt Ï„ z (topspace Ï„) â‡’ open_in Ï„ {z})
Proof
  simp[T2_space_def, door_space_def] >> strip_tac >>
  reverse conj_tac
  >- (simp[not_limpt_inter, SF CONJ_ss, PULL_EXISTS] >> rpt strip_tac
      >- (â€˜{z} = Uâ€™ suffices_by simp[] >>
          qpat_x_assum â€˜_ âˆ© _ = _â€™ mp_tac >>
          simp[EXTENSION] >> metis_tac[OPEN_IN_SUBSET, SUBSET_DEF]) >>
      gs[EXTENSION] >> metis_tac[]) >>
  CCONTR_TAC >> gs[] >> rename [â€˜l1 â‰  l2â€™] >>
  â€˜l1 âˆˆ topspace Ï„ âˆ§ l2 âˆˆ topspace Ï„â€™ by metis_tac[limpt_thm] >>
  first_assum $ drule_all_then $
    qx_choosel_then [â€˜O1â€™, â€˜O2â€™] strip_assume_tac >>
  â€˜Â¬open_in Ï„ {l1} âˆ§ Â¬open_in Ï„ {l2}â€™
    by (gs[limpt_thm] >> metis_tac[IN_INSERT, NOT_IN_EMPTY]) >>
  â€˜âˆƒR1. R1 â‰  âˆ… âˆ§ l1 âˆ‰ R1 âˆ§ O1 = l1 INSERT R1â€™
    by (qexists_tac â€˜O1 DELETE l1â€™ >> simp[] >> metis_tac[]) >>
  â€˜âˆƒR2. R2 â‰  âˆ… âˆ§ l2 âˆ‰ R2 âˆ§ O2 = l2 INSERT R2â€™
    by (qexists_tac â€˜O2 DELETE l2â€™ >> simp[] >> metis_tac[]) >>
  gvs[] >>
  Cases_on â€˜open_in Ï„ (l2 INSERT R1)â€™
  >- (â€˜open_in Ï„ ((l2 INSERT R1) âˆ© (l2 INSERT R2))â€™
        by simp[OPEN_IN_INTER] >>
      â€˜(l2 INSERT R1) âˆ© (l2 INSERT R2) = {l2}â€™
        by (gs[EXTENSION] >> metis_tac[]) >>
      gvs[]) >>
  â€˜l2 INSERT R1 âŠ† topspace Ï„â€™
    by metis_tac[OPEN_IN_SUBSET, SUBSET_DEF, IN_INSERT] >>
  â€˜closed_in Ï„ (l2 INSERT R1)â€™ by metis_tac[] >>
  qabbrev_tac â€˜Q = topspace Ï„ DIFF (l2 INSERT R1)â€™ >>
  â€˜open_in Ï„ Qâ€™ by gs[closed_in] >>
  â€˜open_in Ï„ (Q âˆ© (l1 INSERT R1))â€™ by simp[OPEN_IN_INTER] >>
  â€˜Q âˆ© (l1 INSERT R1) = {l1}â€™ by (gs[EXTENSION, Abbrâ€˜Qâ€™] >> metis_tac[]) >>
  gs[]
QED

Theorem corollary_model:
  istopology { s | 0 âˆˆ s âˆ§ FINITE (COMPL s) âˆ¨ 0 âˆ‰ s }
Proof
  simp[istopology] >> rw[] >> simp[]
  >- (simp[] >> gs[COMPL_DEF] >>
      â€˜UNIV DIFF (s âˆ© t) = (UNIV DIFF s) âˆª (UNIV DIFF t)â€™ suffices_by simp[] >>
      simp[EXTENSION]) >>
  gs[COMPL_DEF] >> Cases_on â€˜âˆ€s. s âˆˆ k â‡’ 0 âˆ‰ sâ€™ >> gs[]
  >- metis_tac[] >>
  gs[SUBSET_DEF] >> first_x_assum drule >> simp[] >> strip_tac >> disj1_tac >>
  conj_tac >- metis_tac[] >>
  irule SUBSET_FINITE_I >> first_assum $ irule_at Any >>
  simp[SUBSET_DEF] >> metis_tac[]
QED

Definition weird_def:
  weird = topology {s | 0 âˆˆ s âˆ§ FINITE (COMPL s) âˆ¨ 0 âˆ‰ s }
End

Theorem open_in_weird:
  open_in weird s â‡” 0 âˆˆ s âˆ§ FINITE (COMPL s) âˆ¨ 0 âˆ‰ s
Proof
  simp[weird_def, topology_tybij |> cj 2 |> iffLR, corollary_model]
QED

Theorem topspace_weird:
  topspace weird = UNIV
Proof
  simp[topspace, Once EXTENSION, open_in_weird] >> gen_tac >>
  qexists_tac â€˜UNIVâ€™ >> simp[COMPL_DEF]
QED

Theorem weird_T2:
  T2_space weird
Proof
  simp[T2_space_def, open_in_weird, topspace_weird] >>
  rpt strip_tac >>
  wlog_tac â€˜b â‰  0â€™ [â€˜aâ€™, â€˜bâ€™] >- (gvs[] >> metis_tac[INTER_COMM]) >>
  Cases_on â€˜a = 0â€™
  >- (qexistsl_tac [â€˜UNIV DELETE bâ€™, â€˜{b}â€™] >>
      simp[COMPL_DEF, DIFF_DIFF_SUBSET, DELETE_DEF] >>
      simp[EXTENSION]) >>
  qexistsl_tac [â€˜{a}â€™, â€˜{b}â€™] >> simp[] >> simp[EXTENSION]
QED

Theorem weird_door:
  door_space weird
Proof
  simp[door_space_def, open_in_weird, topspace_weird, closed_in, COMPL_DEF,
       DIFF_DIFF_SUBSET] >>
  metis_tac[]
QED

Theorem limpt_weird:
  limpt weird x (topspace weird) â‡” x = 0
Proof
  â€˜limpt weird 0 (topspace weird)â€™
    suffices_by metis_tac[exercise_4_1_13_vi, weird_door, weird_T2] >>
  simp[limpt_thm, open_in_weird, topspace_weird, SF CONJ_ss, COMPL_DEF] >>
  rpt strip_tac >>
  â€˜INFINITE Uâ€™ by metis_tac[FINITE_DIFF_down, INFINITE_NUM_UNIV] >>
  â€˜INFINITE (U DELETE 0)â€™ by simp[] >>
  drule INFINITE_INHAB >> simp[]
QED

Theorem exercise_4_1_14:
  second_countable t â‡’
  second_countable (subtopology t Y)
Proof
  rw[second_countable_def] >>
  irule_at Any example_4_1_4 >>
  first_assum $ irule_at Any >>
  `{b âˆ© Y | b âˆˆ B} = IMAGE (Î»x. x âˆ© Y) B`
    suffices_by simp[image_countable] >>
  rw[EXTENSION]
QED

Definition regular_space_def:
  regular_space t â‡”
    âˆ€A x.
      closed_in t A âˆ§
      x âˆˆ topspace t âˆ§
      x âˆ‰ A â‡’
      âˆƒU V.
        open_in t U âˆ§
        open_in t V âˆ§
        x âˆˆ U âˆ§
        A âŠ† V âˆ§
        U âˆ© V = âˆ…
End

Definition T3_space_def:
  T3_space t â‡” regular_space t âˆ§ T1_space t
End

Theorem exercise_4_1_17_i:
  âˆ€A t. regular_space t â‡’ regular_space (subtopology t A)
Proof
  rw[regular_space_def] >>
  gvs[CLOSED_IN_SUBTOPOLOGY, TOPSPACE_SUBTOPOLOGY,
      OPEN_IN_SUBTOPOLOGY, PULL_EXISTS] >>
  rename[`closedSets top A`, `A âˆ© Y`] >>
  first_x_assum drule_all >> rw[] >>
  qexistsl_tac [`U`, `V`] >> rw[]
  >- metis_tac[SUBSET_DEF, IN_INTER]
  >> metis_tac[INTER_COMM, INTER_ASSOC, INTER_EMPTY]
QED

Theorem exercise_4_1_17_iii:
  T3_space t â‡’ T2_space t
Proof
  rw[T3_space_def, T2_space_def,
     regular_space_def, T1_space_def] >>
  `closedSets t {a}` by metis_tac[] >>
  `b âˆ‰ {a}` by simp[] >>
  last_x_assum drule_all >> rw[] >>
  metis_tac[INTER_COMM]
QED


Definition homeomorphism:
  homeomorphism (s,t) (f,g) â‡”
    BIJ f (topspace s) (topspace t) âˆ§
    BIJ g (topspace t) (topspace s) âˆ§
    (âˆ€a. a âˆˆ topspace s â‡’ g (f a) = a) âˆ§
    (âˆ€b. b âˆˆ topspace t â‡’ f (g b) = b) âˆ§
    (âˆ€V. open_in t V â‡’ open_in s (IMAGE g V)) âˆ§
    (âˆ€U. open_in s U â‡’ open_in t (IMAGE f U))
End

Theorem homeomorphism_SYM :
 homeomorphism (s,t) (f,g) â‡” homeomorphism (t,s) (g,f)
Proof
simp[homeomorphism] >> metis_tac[]
QED

Theorem homeomorphism_BIJ0[local] :
 homeomorphism (s,t) (f,g) â‡’
 BIJ f (topspace s) (topspace t)
Proof
  metis_tac[homeomorphism]
QED

Theorem homeomorphism_BIJ :
 homeomorphism (s,t) (f,g) â‡’
 BIJ f (topspace s) (topspace t) âˆ§
 BIJ g (topspace t) (topspace s)
Proof
 metis_tac[homeomorphism_SYM,homeomorphism_BIJ0]
QED

Theorem homeomorphism_TRANS:
  homeomorphism (s,t) (f,g) âˆ§ homeomorphism (t,u) (h,j) â‡’
  homeomorphism (s,u) (h o f, g o j)
Proof
  rw[homeomorphism, GSYM IMAGE_IMAGE]
  >- metis_tac[BIJ_COMPOSE]
  >- metis_tac[BIJ_COMPOSE]
  >- (â€˜f a âˆˆ topspace tâ€™ suffices_by simp[] >>
      metis_tac[BIJ_DEF, SURJ_DEF])
  >- (â€˜j b âˆˆ topspace tâ€™ suffices_by simp[] >>
      metis_tac[BIJ_DEF, SURJ_DEF])
QED

Theorem homeomorphism_REFL:
  homeomorphism (s,s) (I,I)
Proof
  rw[BIJ_DEF, SURJ_DEF, INJ_DEF, homeomorphism]
QED

Theorem homeomorphism_same_domain:
  homeomorphism (t1, t2) (f, g) âˆ§ (âˆ€x. x âˆˆ topspace t1 â‡’ f' x = f x)  â‡’
  homeomorphism (t1, t2) (f', g)
Proof
  rw[homeomorphism] 
  >- (gs[BIJ_DEF, INJ_IFF, SURJ_DEF] >> simp[SF CONJ_ss])
  >- gs[BIJ_DEF, INJ_IFF, SURJ_DEF] 
  >- (â€˜IMAGE f' U = IMAGE f Uâ€™ suffices_by simp[] >>
      irule IMAGE_CONG >> rw[] >> last_x_assum irule >>
      metis_tac[OPEN_IN_SUBSET, SUBSET_DEF])
QED
        
Theorem exercise4_2_6i:
  let fs = {f | âˆƒg. homeomorphism (t, t) (f, g) âˆ§ âˆ€ x. x âˆ‰ topspace t â‡’ f x = x} in
    âˆƒe. e âˆˆ fs âˆ§ (âˆ€f. f âˆˆ fs â‡’ f = f o e) âˆ§ 
        (âˆ€ f. f âˆˆ fs â‡’ âˆƒ g. g âˆˆ fs âˆ§ f o g = e) âˆ§
        (âˆ€f g. f âˆˆ fs âˆ§ g âˆˆ fs â‡’ f o g âˆˆ fs)
Proof
  rw[] >> qexists_tac â€˜Iâ€™ >> rw[]
  >- metis_tac[homeomorphism_REFL]
  >- (qexists_tac â€˜Î»x. if x âˆˆ topspace t then LINV f (topspace t) x else xâ€™ >>
      rw[]
      >- (qexists_tac â€˜fâ€™ >>
          dxrule_then assume_tac (iffLR homeomorphism_SYM) >> 
          drule_then irule homeomorphism_same_domain >> rw[] >>
          gs[homeomorphism, BIJ_DEF] >> metis_tac[LINV_DEF])
      >- (rw[FUN_EQ_THM] >> rw[] >> gs[homeomorphism] >>
          metis_tac[BIJ_LINV_INV])
     )
  >- (simp[] >> metis_tac[homeomorphism_TRANS])
QED

val _ = export_theory();

