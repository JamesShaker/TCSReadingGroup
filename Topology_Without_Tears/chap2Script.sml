open HolKernel Parse boolLib bossLib;

open pred_setTheory intrealTheory;
open topologyTheory chap1Theory chap2_2Theory realTheory;
open arithmeticTheory;
open pairTheory;
open cardinalTheory;

local open realSimps in end

val _ = new_theory "chap2";

val _ = intLib.deprecate_int();

val _ = augment_srw_ss [realSimps.REAL_ARITH_ss]

Definition ival_def:
  ival (r:real) s = { x | r < x âˆ§ x < s }
End

Definition euclidean_def:
  euclidean = topology { s | âˆ€x. x âˆˆ s â‡’ âˆƒa b. x âˆˆ ival a b âˆ§ ival a b âŠ† s}
End

Theorem euclidean_istopology:
  istopology { s | âˆ€x. x âˆˆ s â‡’ âˆƒa b. x âˆˆ ival a b âˆ§ ival a b âŠ† s}
Proof
  simp[istopology, PULL_EXISTS, SUBSET_DEF, ival_def] >> rpt strip_tac
  >- (qpat_x_assum â€˜âˆ€x. x âˆˆ s â‡’ _â€™ $ drule_then $
       qx_choosel_then [â€˜sLâ€™, â€˜sRâ€™] strip_assume_tac >>
      qpat_x_assum â€˜âˆ€x. x âˆˆ t â‡’ _â€™ $ drule_then $
       qx_choosel_then [â€˜tLâ€™, â€˜tRâ€™] strip_assume_tac >>
      qexistsl_tac [â€˜max sL tLâ€™, â€˜min sR tRâ€™] >>
      simp[REAL_MAX_LT, REAL_LT_MIN]) >>
  metis_tac[]
QED

Theorem open_in_euclidean:
  open_in euclidean s â‡” âˆ€x. x âˆˆ s â‡’ âˆƒa b. x âˆˆ ival a b âˆ§ ival a b âŠ† s
Proof
  simp[euclidean_def, topology_tybij |> cj 2 |> iffLR,
       euclidean_istopology]
QED

Theorem topspace_euclidean[simp]:
  topspace euclidean = univ(:real)
Proof
  simp[topspace, Once EXTENSION, open_in_euclidean] >> qx_gen_tac â€˜râ€™ >>
  qexists_tac â€˜UNIVâ€™ >> simp[ival_def] >> qx_gen_tac â€˜sâ€™ >>
  qexistsl_tac [â€˜s - 1â€™, â€˜s + 1â€™] >> simp[]
QED

Theorem ivals_open[simp]:
  open_in euclidean (ival r s)
Proof
  simp[open_in_euclidean] >> metis_tac[SUBSET_REFL]
QED

Theorem neginfr_open[simp]:
  open_in euclidean { x | x < r }
Proof
  simp[open_in_euclidean] >> qx_gen_tac â€˜aâ€™ >> strip_tac >>
  qexistsl_tac [â€˜a - 1â€™, â€˜râ€™] >> simp[ival_def, SUBSET_DEF]
QED

Theorem rposinf_open[simp]:
  open_in euclidean {x | r < x}
Proof
  simp[open_in_euclidean] >> qx_gen_tac â€˜aâ€™ >> strip_tac >>
  qexistsl_tac [â€˜râ€™, â€˜a + 1â€™] >> simp[ival_def, SUBSET_DEF]
QED

Theorem closed_not_open:
  (* text has c < d as assumption *)
  c â‰¤ d â‡’ Â¬open_in euclidean { x | c â‰¤ x âˆ§ x â‰¤ d }
Proof
  simp[open_in_euclidean] >> strip_tac >> qexists_tac â€˜câ€™ >>
  simp[] >> CCONTR_TAC >> gs[SUBSET_DEF, ival_def] >>
  first_x_assum $ qspec_then â€˜(a + c) / 2â€™ mp_tac >>
  simp[]
QED



Theorem closed_is_closed[simp]:
  closed_in euclidean { x | c â‰¤ x âˆ§ x â‰¤ d }
Proof
  simp[closed_in] >>
  â€˜open_in euclidean {x | x < c} âˆ§ open_in euclidean {x | d < x}â€™
    by simp[] >>
  dxrule_all OPEN_IN_UNION >>
  â€˜{x | d < x} âˆª {x | x < c} = UNIV DIFF {x | c â‰¤ x âˆ§ x â‰¤ d}â€™
    suffices_by simp[] >>
  simp[EXTENSION]
QED

Theorem singles_closed:
  closed_in euclidean {x}
Proof
  â€˜{x} = {a | x â‰¤ a âˆ§ a â‰¤ x}â€™ by simp[EXTENSION] >>
  simp[]
QED

Theorem real_of_int_addN:
  real_of_int i + &n = real_of_int (i + &n)
Proof
  Cases_on â€˜iâ€™ >> simp[]
QED

Theorem real_of_int_LTN[simp]:
  real_of_int i < &j â‡” i < &j
Proof
  Cases_on â€˜iâ€™ >> simp[]
QED

Theorem real_of_int_NLT[simp]:
  &i < real_of_int j â‡” &i < j
Proof
  Cases_on â€˜jâ€™ >> simp[]
QED

Theorem integers_closed:
  closed_in euclidean { real_of_int i | T }
Proof
  simp[closed_in] >>
  â€˜UNIV DIFF {real_of_int i | T} =
   BIGUNION { ival (real_of_int i) (real_of_int (i + 1)) | T }â€™
    by (simp[Once EXTENSION, PULL_EXISTS] >> qx_gen_tac â€˜râ€™ >> eq_tac
        >- (simp[ival_def] >> strip_tac >> qexists_tac â€˜flr râ€™ >>
            simp[INT_FLOOR_BOUNDS, real_of_int_addN,
                 Excl "real_of_int_add"] >>
            assume_tac (cj 1 INT_FLOOR_BOUNDS) >>
            pop_assum $ qspec_then â€˜râ€™ mp_tac >> simp[REAL_LE_LT]) >>
        simp[ival_def] >> rpt strip_tac >>
        gs[real_of_int_addN, Excl "real_of_int_add"] >>
        intLib.ARITH_TAC) >>
  simp[] >> irule OPEN_IN_BIGUNION >> simp[PULL_EXISTS]
QED

(* next: between any two rational numbers there is an irrational number *)

Definition rational_def:
rational q â‡” âˆƒa:int b:int. q = real_of_int a / real_of_int b âˆ§ b â‰  0
End

Theorem rational_Nats[simp]:
  rational (&n)
Proof
  simp[rational_def] >> qexistsl_tac [â€˜&nâ€™, â€˜1â€™] >> simp[]
QED

Theorem rational_Ints[simp]:
  rational (real_of_int i)
Proof
  simp[rational_def] >> qexistsl_tac [â€˜iâ€™, â€˜1â€™] >> simp[]
QED

Theorem real_of_int_eq_n[simp]:
  (real_of_int i = &n â‡” i = &n) âˆ§ (&n = real_of_int i â‡” i = &n) âˆ§
  (real_of_int i = -&n â‡” i = -&n) âˆ§ (-&n = real_of_int i â‡” i = -&n)
Proof
   Cases_on â€˜iâ€™ >> simp[]
QED

Theorem rational_ADD_I:
rational a âˆ§ rational b â‡’ rational (a + b)
Proof
  rw[rational_def] >> simp[REAL_ADD_RAT,GSYM real_of_int_mul,Excl "real_of_int_mul",
                          GSYM real_of_int_add,Excl "real_of_int_add"] >>
  irule_at Any EQ_REFL >> simp[]
QED

Theorem rational_neg[simp]:
rational (- a) â‡” rational a
Proof
  â€˜âˆ€a. rational a â‡’ rational (- a)â€™
    suffices_by metis_tac[REAL_NEG_NEG] >>
  rw[rational_def] >> simp[neg_rat,GSYM real_of_int_neg,Excl "real_of_int_neg"]  >>
  metis_tac[]
QED

Theorem irrational_ADD_I:
  rational a âˆ§ Â¬rational b â‡’ Â¬rational (a + b)
Proof
  CCONTR_TAC >> gs[] >>
  â€˜rational (-a)â€™ by simp[] >>
  â€˜a + b + (-a) = bâ€™ by simp[] >> metis_tac[rational_ADD_I]
QED

Theorem irrational_ADD_I' = ONCE_REWRITE_RULE [REAL_ADD_COMM] irrational_ADD_I

Theorem rational_MULT_I:
  rational a âˆ§ rational b â‡’ rational (a * b)
Proof
  rw[rational_def] >>
  simp[mult_rat,Excl "REALMULCANON",Excl "RMUL_EQNORM1",Excl "RMUL_EQNORM2",
      GSYM real_of_int_mul,Excl "real_of_int_mul"] >>
  irule_at Any EQ_REFL >> simp[]
QED

Theorem rational_inv[simp]:
  rational (inv a) â‡” rational a
Proof
  â€˜âˆ€a. rational a â‡’ rational (inv a)â€™
    suffices_by metis_tac[REAL_INV_INV] >>
  rw[rational_def] >>
  rename [â€˜real_of_int a / real_of_int bâ€™] >>
  Cases_on â€˜a = 0â€™ (* 2 *)
  >- (simp[REAL_DIV_ZERO] >> metis_tac[]) >>
  simp[real_div,Excl "REALMULCANON",Excl "RMUL_EQNORM1",Excl "RMUL_EQNORM2",
      REAL_INV_MUL',REAL_INV_INV] >>
  metis_tac[REAL_MUL_COMM]
QED

Theorem rational_DIV_I:
  rational a âˆ§ rational b â‡’ rational (a/b)
Proof
  simp[real_div, rational_MULT_I]
QED



Theorem irrational_MULT_I:
  a â‰  0 âˆ§ rational a âˆ§ Â¬rational b â‡’ Â¬rational (a * b)
Proof
  CCONTR_TAC >> gs[] >>
  â€˜rational (inv a)â€™ by simp[] >>
  â€˜a * b * (inv a) = bâ€™ by simp[] >> metis_tac[rational_MULT_I]
QED

Theorem irrational_MULT_I' = ONCE_REWRITE_RULE [REAL_MUL_COMM] irrational_MULT_I

Theorem irrational_DIVNUM_I:
  Â¬rational a âˆ§ rational b âˆ§ b â‰  0 â‡’ Â¬rational (a/b)
Proof
  rpt strip_tac >>
  â€˜rational (a/b * b)â€™ by simp[rational_MULT_I] >>
  gs[]
QED

Theorem irrational_DIVDEN_I:
  rational a âˆ§ Â¬rational b âˆ§ a â‰  0 â‡’ Â¬rational (a/b)
Proof
  rpt strip_tac >>
  â€˜rational (inv (a/b))â€™ by simp[] >>
  fs[real_div, Excl "rational_inv", REAL_INV_MUL', REAL_INV_INV] >>
  â€˜rational (a * (inv a * b))â€™ by simp[rational_MULT_I] >> gs[]
QED

Theorem positive_rationals_natural:
  rational a âˆ§ 0 < a â‡’ âˆƒm n. n â‰  0 âˆ§ a = &m / &n
Proof
  rw[rational_def] >> rename [`0 < real_of_int a / real_of_int b`] >>
  Cases_on `a` >> Cases_on `b` >> REWRITE_TAC [real_div] >>
  RULE_ASSUM_TAC (REWRITE_RULE [real_div]) >> gs[]
  >- (rename[`&a = (&_)â»Â¹ * &(_ * b)`] >> qexistsl_tac [`a`, `b`] >>
      simp[]) >> rename[â€˜&a = inv _ * &( _ * b) â€™] >>
  qexistsl_tac [`a`, `b`] >> simp[REAL_NEG_INV]
QED

Theorem sqrt_2_irrational:
  Â¬rational (sqrt 2)
Proof
  strip_tac >> drule positive_rationals_natural >> simp[SQRT_POS_LT] >>
  rpt strip_tac >> CCONTR_TAC >>
  qabbrev_tac `k = LEAST k. âˆƒj. j â‰  0 âˆ§ &k / &j = sqrt 2` >>
  `âˆƒj. j â‰  0 âˆ§ &k / &j = sqrt 2`
    by (simp[Abbr `k`] >> numLib.LEAST_ELIM_TAC >> rw[] >> metis_tac[]) >>
  `âˆ€a b. b â‰  0 âˆ§ &a / &b = sqrt 2 â‡’ k â‰¤ a`
    by (simp[Abbr `k`] >> numLib.LEAST_ELIM_TAC >> rw[] >>
        metis_tac[DECIDE ``x:num <= y â‡” Â¬ (y < x)``]) >>
  `(sqrt 2) pow 2 = 2` by simp[SQRT_POW2] >>
  `0 < k`
    by (CCONTR_TAC >> gs[realTheory.SQRT_POS_NE]) >>
  `&k = sqrt 2 * &j` by gvs[real_div] >>
  `&k pow 2 = (sqrt 2 * &j) pow 2` by simp[] >>
  qpat_x_assum `&k = _` $ K all_tac >> gs[POW_MUL, REAL_OF_NUM_POW] >>
  `EVEN (k ** 2)` by metis_tac[EVEN_EXISTS] >> gs[EVEN_EXP_IFF] >>
  `âˆƒk0. k = k0 * 2` by gs[EVEN_EXISTS] >> qpat_x_assum `Abbrev _` $ K all_tac >>
  gvs[EXP_BASE_MULT] >> `2 * (k0 ** 2) = j ** 2` by simp[] >>
  `EVEN (j ** 2)` by metis_tac[EVEN_EXISTS] >> gs[EVEN_EXP_IFF] >>
  `âˆƒj0. j = j0 * 2` by gs[EVEN_EXISTS] >> gvs[EXP_BASE_MULT] >>
  `k0 ** 2 = 2 * j0 ** 2` by simp[] >>
  `&(k0 ** 2) / &(j0 ** 2) = 2r`
    by (simp[real_div] >> simp[MULT_DIV]) >>
  `sqrt ( & (k0 ** 2) / & (j0 ** 2)) = sqrt 2` by simp[] >>
  qpat_x_assum `_ = 2r` $ K all_tac >>
  gs[GSYM REAL_OF_NUM_POW, SQRT_DIV, POW_2_SQRT] >>
  `2 * k0 â‰¤ k0` by metis_tac[] >> gs[]
QED

Theorem sqrt2_bounds[simp]:
  0 < sqrt 2 âˆ§ sqrt 2 < 2
Proof
  conj_tac >- simp[SQRT_POS_LT] >>
  CCONTR_TAC >> gs[REAL_NOT_LT] >>
  drule_at (Pos last) POW_LE >> simp[] >>
  qexists_tac â€˜2â€™ >> simp[SQRT_POW_2]
QED


Theorem rationals_bracket_irrationals:
  âˆ€a b. rational a âˆ§ rational b âˆ§ a < b â‡’ âˆƒc. Â¬rational c âˆ§ a < c âˆ§ c < b
Proof
  rw[] >> qexists_tac â€˜(b - a) * sqrt 2 / 2 + aâ€™ >> rpt conj_tac
  >- (ONCE_REWRITE_TAC [REAL_ADD_COMM] >> irule irrational_ADD_I >>
      simp[] >> REWRITE_TAC[real_div] >>
      irule irrational_MULT_I' >> simp[] >>
      irule irrational_MULT_I' >> simp[real_sub] >>
      simp[rational_ADD_I, sqrt_2_irrational])
  >- (simp[REAL_LT_ADDL] >> irule REAL_LT_MUL >> simp[]) >>
  simp[GSYM REAL_LT_SUB_LADD]
QED

Theorem density_lemma'[local]:
  Â¬rational a âˆ§ rational b â‡’
  (a < b â‡’ âˆƒc. Â¬rational c âˆ§ a < c âˆ§ c < b) âˆ§
  (b < a â‡’ âˆƒc. Â¬rational c âˆ§ b < c âˆ§ c < a)
Proof
  strip_tac >> wlog_tac â€˜a < bâ€™ [â€˜aâ€™, â€˜bâ€™]
  >- (gs[] >> strip_tac >>
      first_x_assum $ qspecl_then [â€˜-aâ€™, â€˜-bâ€™] mp_tac >> simp[] >>
      disch_then $ qx_choose_then â€˜c0â€™ strip_assume_tac >>
      qexists_tac â€˜-c0â€™ >> simp[]) >>
  simp [] >> qexists_tac â€˜(a + b) / 2â€™ >> simp[] >>
  simp[irrational_ADD_I', irrational_DIVNUM_I]
QED

Theorem irrationals_dense:
  âˆ€a b. a < b â‡’ âˆƒr. Â¬rational r âˆ§ a < r âˆ§ r < b
Proof
  rpt strip_tac >>
  map_every Cases_on [â€˜rational aâ€™, â€˜rational bâ€™]
  >- metis_tac[rationals_bracket_irrationals]
  >- metis_tac[density_lemma']
  >- metis_tac[density_lemma'] >>
  qabbrev_tac â€˜m = (a + b) / 2â€™ >>
  â€˜a < m âˆ§ m < bâ€™ by simp[Abbrâ€˜mâ€™] >>
  Cases_on â€˜rational mâ€™ >> simp[SF SFY_ss] >>
  drule_all (cj 1 density_lemma') >> metis_tac[REAL_LT_TRANS]
QED

Theorem rationals_dense:
  âˆ€a b. a < b â‡’ âˆƒr. rational r âˆ§ a < r âˆ§ r < b
Proof
  rpt strip_tac >>
  â€˜0 < b - aâ€™ by simp[] >>
  dxrule_then (qspec_then â€˜1â€™ $ qx_choose_then â€˜mâ€™ assume_tac) REAL_ARCH >>
  gs[REAL_SUB_LDISTRIB] >>
  â€˜a * &m + 1 < b * &mâ€™ by simp[] >>
  drule_then (qx_choose_then â€˜dâ€™ strip_assume_tac) ints_exist_in_gaps >>
  â€˜m â‰  0â€™ by (strip_tac >> gs[]) >>
  â€˜a < real_of_int d / &m âˆ§ real_of_int d / &m < bâ€™ by simp[] >>
  â€˜rational (real_of_int d / &m)â€™ by simp[rational_DIV_I] >>
  metis_tac[]
QED

Theorem rationals_not_open_euclidean:
  Â¬open_in euclidean { r | rational r }
Proof
  simp[open_in_euclidean] >> qexists_tac â€˜0â€™ >> simp[] >>
  rpt gen_tac >> reverse (Cases_on â€˜a < bâ€™)
  >- simp[ival_def] >>
  disj2_tac >> simp[SUBSET_DEF, ival_def] >>
  metis_tac[irrationals_dense]
QED

Theorem rationals_not_closed_euclidean:
  Â¬closed_in euclidean { r | rational r }
Proof
  simp[closed_in, open_in_euclidean] >> qexists_tac â€˜sqrt 2â€™ >>
  simp[sqrt_2_irrational] >> rpt strip_tac >>
  Cases_on â€˜a < bâ€™ >> simp[ival_def, SUBSET_DEF] >>
  metis_tac[rationals_dense]
QED

Theorem exercise_2_1_1:
  a < b â‡’ Â¬open_in euclidean { r | a â‰¤ r âˆ§ r < b } âˆ§
          Â¬closed_in euclidean {r  | a â‰¤ r âˆ§ r < b } âˆ§
          Â¬open_in euclidean {r | a < r âˆ§ r â‰¤ b } âˆ§
          Â¬closed_in euclidean { r | a < r âˆ§ r â‰¤ b}
Proof
  rw[open_in_euclidean, closed_in]
  >- (qexists_tac â€˜aâ€™ >> simp[] >> qx_genl_tac [â€˜câ€™, â€˜dâ€™] >>
      Cases_on â€˜a âˆˆ ival c dâ€™ >> simp[] >>
      gs[ival_def, SUBSET_DEF] >>
      â€˜âˆƒac. c < ac âˆ§ ac < aâ€™ by metis_tac[rationals_dense] >>
      qexists_tac â€˜acâ€™ >> simp[])
  >- (simp[ival_def, SUBSET_DEF] >> qexists_tac â€˜bâ€™ >> simp[]>>
      qx_genl_tac [â€˜câ€™, â€˜dâ€™] >>
      map_every Cases_on [â€˜c < bâ€™, â€˜b < dâ€™] >> simp[] >>
      â€˜âˆƒcb. max a c < cb âˆ§ cb < bâ€™
        by metis_tac[rationals_dense, REAL_MAX_LT]>>
      qexists_tac â€˜cbâ€™ >> gs[REAL_MAX_LT])
  >- (qexists_tac â€˜bâ€™ >> simp[ival_def, SUBSET_DEF] >>
      qx_genl_tac [â€˜câ€™, â€˜dâ€™] >>
      map_every Cases_on [â€˜c < bâ€™, â€˜b < dâ€™] >> simp[] >>
      â€˜âˆƒbd. b < bd âˆ§ bd < dâ€™ by metis_tac[rationals_dense] >>
      qexists_tac â€˜bdâ€™ >> simp[])
  >- (simp[ival_def, SUBSET_DEF] >> qexists_tac â€˜aâ€™ >> simp[]>>
      qx_genl_tac [â€˜câ€™, â€˜dâ€™] >>
      map_every Cases_on [â€˜c < aâ€™, â€˜a < dâ€™] >> simp[] >>
      â€˜âˆƒab. a < ab âˆ§ ab < min b dâ€™
        by metis_tac[rationals_dense, REAL_LT_MIN]>>
      qexists_tac â€˜abâ€™ >> gs[REAL_LT_MIN])
QED

Theorem exercise_2_1_2:
  closed_in euclidean { r | a â‰¤ r } âˆ§
  closed_in euclidean { r | r â‰¤ a }
Proof
  simp[closed_in] >>
  â€˜UNIV DIFF {r | a â‰¤ r } = {r | r < a} âˆ§
   UNIV DIFF {r | r â‰¤ a } = {r | a < r}â€™
    suffices_by simp[] >>
  simp[EXTENSION]
QED

Theorem exercise_2_1_3:
  âˆƒA. (âˆ€a. a âˆˆ A â‡’ closed_in euclidean a) âˆ§
      INFINITE A âˆ§
      Â¬closed_in euclidean (BIGUNION A)
Proof
  qexists_tac â€˜{ {r} | 0 < r âˆ§ r < 1 }â€™>>
  simp[PULL_EXISTS, singles_closed] >>
  simp[closed_in] >> conj_tac
  >- (simp[infinite_num_inj] >>
      qexists_tac â€˜Î»n. {inv (&(n + 2))}â€™  >>
      simp[INJ_IFF]) >>
  â€˜BIGUNION {{r} | 0 < r âˆ§ r < 1} = ival 0 1â€™
    by simp[Once EXTENSION, PULL_EXISTS, ival_def] >>
  simp[] >> simp[open_in_euclidean] >>
  qexists_tac â€˜0â€™ >> simp[ival_def, SUBSET_DEF] >>
  qx_genl_tac [â€˜câ€™, â€˜dâ€™] >>
  map_every Cases_on  [â€˜c < 0â€™, â€˜0 < dâ€™] >> simp[] >>
  â€˜âˆƒd0. 0 < d0 âˆ§ d0 < min 1 dâ€™
    by metis_tac[rationals_dense, REAL_MIN_LT, REAL_LT_MIN,
                 REAL_LT_01] >>
  qexists_tac â€˜d0â€™ >> gs[REAL_LT_MIN]
QED


Theorem real_of_int_Nmul:
 &n * real_of_int i = real_of_int (&n * i)
Proof
 Cases_on â€˜iâ€™ >> simp[]
QED

Theorem real_of_int_NLE[simp]:
 &i â‰¤ real_of_int j â‡” &i â‰¤ j
Proof
  Cases_on â€˜jâ€™ >> simp[integerTheory.INT_NOT_LE]
QED

Theorem exercise_2_1_4i:
  Â¬open_in euclidean { real_of_int i | T }
Proof
  simp[open_in_euclidean, PULL_EXISTS] >>
  qexists_tac â€˜0â€™ >> qx_genl_tac [â€˜câ€™, â€˜dâ€™] >>
  Cases_on â€˜real_of_int 0 âˆˆ ival c dâ€™ >> simp[] >>
  gs[ival_def, SUBSET_DEF] >>
  Cases_on â€˜1 â‰¤ dâ€™
  >- (qexists_tac â€˜1 / 2â€™ >> simp[] >>
      â€˜0 < 1/2 âˆ§ 1/2 < 1â€™ by simp[] >>
      rpt strip_tac >>
      pop_assum (SUBST_ALL_TAC o SYM) >>
      gs[] >>
      qspecl_then [â€˜0â€™, â€˜iâ€™] mp_tac integerTheory.INT_DISCRETE >>
      simp[])
  >- (qexists_tac â€˜d / 2â€™ >> simp[] >>
      REWRITE_TAC [real_div] >> simp[] >> rpt strip_tac >> gvs[] >>
      gs[real_of_int_Nmul,Excl "real_of_int_mul"] >>
      pop_assum mp_tac >> intLib.ARITH_TAC)
QED

Theorem ival_without_int:
  âˆ€x. (Â¬âˆƒz:int. x = real_of_int z) â‡’
      âˆƒa b. x âˆˆ ival a b âˆ§ âˆ€z:int. real_of_int z âˆ‰ ival a b
Proof
  simp[ival_def] >> rpt strip_tac >>
  wlog_tac â€˜0 â‰¤ xâ€™ [â€˜xâ€™]
  >- (first_x_assum $ qspec_then â€˜-xâ€™ assume_tac >> gvs[] >>
      â€˜âˆ€z. -x â‰  real_of_int zâ€™
        by (rpt strip_tac >>
            metis_tac[REAL_NEG_NEG,real_of_int_neg]) >>
      first_x_assum drule >> strip_tac >>
      qexistsl_tac [â€˜-bâ€™,â€˜-aâ€™] >> gvs[] >> strip_tac >>
      first_x_assum $ qspec_then â€˜-zâ€™ mp_tac >> rpt strip_tac >>
      gvs[])
  >- (qexistsl_tac [â€˜&(flr x)â€™,â€˜&(clg x)â€™] >> rpt strip_tac (* 3 *)
      >- (gvs[REAL_LT_LE,NUM_FLOOR_LE] >> strip_tac >>
          metis_tac[real_of_int_num])
      >- (gvs[REAL_LT_LE,LE_NUM_CEILING] >> metis_tac[real_of_int_num]) >>
      CCONTR_TAC >> fs[] >> Cases_on â€˜x â‰¤ 0 âˆ¨ x = &flr xâ€™ >> gvs[] (* 3 *)
      >- (â€˜x = 0â€™ by gvs[] >> metis_tac[real_of_int_num])
      >- metis_tac[integerTheory.INT_LT_ANTISYM] >>
      irule integerTheory.INT_DISCRETE >>
      â€˜&(flr x + 1) = (&flr x) + 1:intâ€™
        by gvs[GSYM integerTheory.INT_ADD] >> metis_tac[])
QED

Theorem real_of_int_subN:
  real_of_int i - &n = real_of_int (i - &n)
Proof
  Cases_on â€˜iâ€™ >> simp[]
QED

Theorem exercise_2_1_4ii:
  closed_in euclidean {real_of_int i | P i}
Proof
  simp[closed_in,open_in_euclidean] >> rpt strip_tac >>
  Cases_on â€˜âˆƒz:int. x = real_of_int zâ€™
  >- (gvs[] >> qexistsl_tac [â€˜real_of_int z - 1/2â€™,â€˜real_of_int z + 1/2â€™] >>
      simp[ival_def,SUBSET_DEF] >> rpt strip_tac
      >- simp[REAL_LT_SUB_RADD]
      >> gvs[] >>
      rename [â€˜real_of_int a - 1 / 2 < real_of_int bâ€™] >>
      â€˜a = bâ€™ suffices_by (strip_tac >> gvs[]) >>
      â€˜2 * real_of_int a - 1 < 2 * real_of_int b âˆ§ 2 * real_of_int b < 2 * real_of_int a + 1â€™
        by
        (strip_tac >> rev_drule REAL_LT_LMUL_IMP >> strip_tac >>
         first_x_assum $ qspec_then â€˜2â€™ mp_tac >> strip_tac >>
         â€˜0r < 2râ€™ by simp[] >> first_x_assum drule >> strip_tac
         >- (â€˜2 * (real_of_int a âˆ’ 1 / 2) = 2 * real_of_int a âˆ’ 1â€™ suffices_by metis_tac[] >>
             simp[REAL_SUB_LDISTRIB])
         >- (â€˜2 * real_of_int b < 2 * (real_of_int a + 1 / 2)â€™  by simp[REAL_LT_LMUL_IMP] >>
             â€˜2 * (real_of_int a + 1 / 2) = 2 * real_of_int a + 1â€™ suffices_by metis_tac[] >>
             simp[REAL_ADD_LDISTRIB])) >>
      gs[real_of_int_Nmul, Excl "real_of_int_mul", real_of_int_addN, Excl "real_of_int_add",
         real_of_int_subN, Excl "real_of_int_sub"]>>
      intLib.ARITH_TAC) >>
  drule ival_without_int >> strip_tac >>
  qexistsl_tac [â€˜aâ€™,â€˜bâ€™] >> gvs[SUBSET_DEF] >>
  rpt strip_tac >> metis_tac[]
QED

Theorem exercise_2_1_4iiN:
  closed_in euclidean {&(n:num) | P n}
Proof
  assume_tac (exercise_2_1_4ii |> Q.INST [â€˜Pâ€™ |-> â€˜Î»i. 0 â‰¤ i âˆ§ Q (Num i)â€™]
                               |> Q.GEN â€˜Qâ€™) >>
  pop_assum $ qspec_then â€˜Pâ€™ mp_tac >>
  simp[] >> qmatch_abbrev_tac â€˜closed_in euclidean S1 â‡’ closed_in _ S2â€™ >>
  â€˜S1 = S2â€™ suffices_by simp[] >>
  simp[Abbrâ€˜S1â€™, Abbrâ€˜S2â€™, EXTENSION] >>
  rw[EQ_IMP_THM] >~
  [â€˜P (Num i)â€™] >- (Cases_on â€˜iâ€™ >> gs[]) >~
  [â€˜&n = real_of_int _â€™] >>
  qexists_tac â€˜&nâ€™ >> simp[]
QED

Theorem ival_11[simp]:
  a < b âˆ§ c < d â‡’
  (ival a b = ival c d â‡” a = c âˆ§ b = d)
Proof
  simp[EXTENSION, ival_def, Once EQ_IMP_THM] >>
  wlog_tac `a <= c` [`a`, `c`, `b`, `d`] (* 2 *)
  >- (â€˜c â‰¤ aâ€™ by fs[] >> metis_tac[]) >>
  Cases_on â€˜a = câ€™ (* 2 *)
  >-  (rw[] >> CCONTR_TAC >>
       Cases_on â€˜b < dâ€™
       >- (â€˜âˆƒz. b < z âˆ§ z < dâ€™ by metis_tac[REAL_MEAN] >>
           first_x_assum (qspec_then â€˜zâ€™ assume_tac) >> fs[]) >>
       â€˜d < bâ€™ by fs[] >>
       â€˜âˆƒz. d < z âˆ§ z < bâ€™ by metis_tac[REAL_MEAN] >>
       first_x_assum (qspec_then â€˜zâ€™ assume_tac) >> fs[]) >>
  (â€˜a < câ€™ by fs[] >> rw[] >>
   â€˜a < min b câ€™ by simp[REAL_LT_MIN] >>
   â€˜âˆƒz. a < z âˆ§ z < min b câ€™ by metis_tac[REAL_MEAN] >>
   qexists_tac â€˜zâ€™ >> rw[] >>
   Cases_on â€˜z < bâ€™ >> rw[] (* 2, have the option of avoid cases here*)
   >- (â€˜z < câ€™ suffices_by fs[] >> metis_tac[REAL_LT_MIN]) >>
   metis_tac[REAL_LT_MIN] >>
   metis_tac[REAL_LT_MIN,REAL_LT_TRANS])
QED

Theorem prop2_2_1:
  open_in euclidean s â‡” âˆƒP. s = BIGUNION { ival a b | P a b }
Proof
  simp[EQ_IMP_THM, PULL_EXISTS, OPEN_IN_BIGUNION] >>
  simp[open_in_euclidean] >> strip_tac >>
  gs[GSYM RIGHT_EXISTS_IMP_THM, SKOLEM_THM] >>
  rename [â€˜ival (F1 _) (F2 _)â€™] >>
  qexists_tac â€˜Î»a b. âˆƒx. a = F1 x âˆ§ b = F2 x âˆ§ x âˆˆ sâ€™ >>
  simp[Once EXTENSION] >> simp[EQ_IMP_THM] >> rpt strip_tac >>
  simp[PULL_EXISTS] >- metis_tac[] >>
  gvs[] >> first_x_assum drule >>
  metis_tac[SUBSET_DEF]
QED

(* example 2.2.3 *)
Theorem ivals_basis:
  basis { ival a b | T } euclidean
Proof
  simp[basis_def, PULL_EXISTS] >> rpt strip_tac >>
  drule_then strip_assume_tac (iffLR prop2_2_1) >>
  simp[] >> irule_at Any EQ_REFL >> simp[SUBSET_DEF, PULL_EXISTS] >>
  metis_tac[]
QED

Definition open_rectangle:
  open_rectangle (a: real) b c d = {(x, y) | a < x âˆ§ x < b âˆ§ c < y âˆ§ y < d}
End

Definition open_rectangles:
  open_rectangles = {open_rectangle a b c d | a < b âˆ§ c < d}
End

(* example 2.2.9 *)
Theorem open_rectangle_topology_exists:
  (* triangle inequality implies *)
  âˆƒt. topspace t = UNIV âˆ§ basis open_rectangles t
Proof
  simp[prop_2_2_8] >> rw[] (* 2 *)
  >- (simp[Once EXTENSION, open_rectangles, PULL_EXISTS, open_rectangle, FORALL_PROD] >>
      qx_genl_tac [`x`, `y`] >> qexistsl_tac [`x-1`, `x+1`, `y-1`, `y+1`] >>
      simp[])
  >> Cases_on `b1 âˆ© b2 = âˆ…` (* 2 *)
  >- (qexists_tac `âˆ…` >> simp[])
  >> gvs[open_rectangles] >>
  rename [`open_rectangle a b c d âˆ© open_rectangle h i j k â‰  âˆ…`] >>
  wlog_tac `a â‰¤ h` [`a`, `b`, `c`, `d`, `h`, `i`, `j`, `k`] (* 2 *)
  >- (`h â‰¤ a` by simp[] >> metis_tac[INTER_COMM])
  >> `h < b`
        by (CCONTR_TAC >> gs[REAL_NOT_LT] >> qpat_x_assum `_ âˆ© _ â‰  _` mp_tac >>
            simp[EXTENSION, open_rectangle, FORALL_PROD]) >>
  Cases_on `c â‰¤ j` (* 2 *)
  >- (`j < d`
        by (CCONTR_TAC >> gs[REAL_NOT_LT] >> qpat_x_assum `_ âˆ© _ â‰  _` mp_tac >>
            simp[EXTENSION, open_rectangle, FORALL_PROD]) >>
      qexists_tac `{open_rectangle h (min b i) j (min d k)}` >> simp[] >> conj_tac
      >- (irule_at Any EQ_REFL >> simp[REAL_LT_MIN])
      >> simp[open_rectangle, EXTENSION, FORALL_PROD] >> qx_genl_tac [`x`, `y`] >>
      simp[EQ_IMP_THM, REAL_LT_MIN])
  >> gs[REAL_NOT_LE] >>
  `c < k`
    by (CCONTR_TAC >> gs[REAL_NOT_LT] >> qpat_x_assum `_ âˆ© _ â‰  _` mp_tac >>
            simp[EXTENSION, open_rectangle, FORALL_PROD]) >>
  qexists_tac `{open_rectangle h (min b i) c (min d k)}` >> simp[] >> conj_tac
      >- (irule_at Any EQ_REFL >> simp[REAL_LT_MIN])
      >> simp[open_rectangle, EXTENSION, FORALL_PROD] >> qx_genl_tac [`x`, `y`] >>
      simp[EQ_IMP_THM, REAL_LT_MIN]
QED

Theorem negx_squared[simp]:
  (-a) pow 2 = a pow 2
Proof
  simp[Once REAL_NEG_MINUS1]
QED

Definition tri_ineq:
  tri_ineq = âˆ€x y u v a b. sqrt ((x - a) pow 2 + (y - b) pow 2) â‰¤
                           sqrt ((x - u) pow 2 + (y - v) pow 2) +
                           sqrt ((u - a) pow 2 + (v - b) pow 2)
End

Definition D_def:
  D = {(x,y) | x pow 2 + y pow 2 < 1 }
End

Definition R_def:
  R a b =
  let r = sqrt (a pow 2 + b pow 2);
      d = (1 - r)/8
  in open_rectangle (a - d) (a + d) (b - d) (b + d)
End

Theorem R_includes_centre:
  âˆ€a b. (a, b) âˆˆ D â‡’ (a, b) âˆˆ R a b
Proof
  simp[R_def, open_rectangle, D_def] >> rw[] >>
  simp[REAL_LT_SUB_RADD, REAL_SUB_LT] >> simp[Once (GSYM SQRT_1)] >>
  irule SQRT_MONO_LT >> rw[REAL_LE_ADD]
QED

Theorem exercise_2_2_1_i:
  tri_ineq â‡’
  âˆ€a b. (a,b) âˆˆ D â‡’ R a b âŠ† D
Proof
  SRW_TAC [] [D_def,R_def] >> irule SUBSET_TRANS >>
  qexists_tac â€˜{(x,y)| sqrt((x-a) pow 2 + (y - b) pow 2) < 1 - r}â€™ >>
  conj_tac (*2*)
  >- (simp[SUBSET_DEF,open_rectangle,PULL_EXISTS] >>
      qabbrev_tac â€˜s = 1 - râ€™ >> qx_genl_tac [â€˜xâ€™,â€˜yâ€™] >> strip_tac >>
      irule REAL_LET_TRANS >>
      qexists_tac â€˜sqrt (d pow 2 + d pow 2)â€™ >> strip_tac (* 2 *)
      >- (simp[Abbrâ€˜dâ€™] >>
          â€˜sÂ² / 64 + sÂ² / 64 = s pow 2 / 32â€™
            by
            (simp[REAL_DIV_ADD,REAL_DOUBLE] >>
             REWRITE_TAC [real_div] >> simp[]) >>
          simp[SQRT_DIV] >>
          â€˜0 â‰¤ sâ€™
            by (simp[Abbrâ€˜sâ€™,Abbrâ€˜râ€™,REAL_SUB_LE] >>
                SUBST1_TAC (GSYM SQRT_1) >> irule SQRT_MONO_LE >>
                simp[REAL_LE_POW2,REAL_LE_ADD]) >>
          simp[POW_2_SQRT,REAL_LT_LDIV_EQ,SQRT_POS_LT] >>
          â€˜0 < s âˆ§ 1 < sqrt 32â€™ suffices_by simp[] >>
          reverse conj_tac
          >- (â€˜0r â‰¤ 1â€™ by simp[] >> dxrule SQRT_MONO_LT >> simp[SQRT_1])>>
          simp[Abbrâ€˜râ€™, Abbrâ€˜sâ€™, REAL_SUB_LT] >>
          ONCE_REWRITE_TAC [GSYM SQRT_1] >>
          irule SQRT_MONO_LT >> simp[REAL_LE_POW2, REAL_LE_ADD]) >>
      irule SQRT_MONO_LE >> simp[REAL_LE_POW2, REAL_LE_ADD] >>
      irule REAL_LE_ADD2 >> conj_tac
      >- (Cases_on â€˜a â‰¤ xâ€™ >- (irule POW_LE >> simp[]) >>
          â€˜(x - a)Â² = (a - x)Â²â€™
            suffices_by (simp[] >> strip_tac >>
                         irule POW_LE >> simp[]) >>
          â€˜a - x = -(x-a)â€™ by simp[] >> simp[]) >>
      Cases_on â€˜b â‰¤ yâ€™ >- (irule POW_LE >> simp[]) >>
      â€˜(y - b)Â² = (b - y)Â²â€™
        suffices_by (simp[] >> strip_tac >>
                     irule POW_LE >> simp[]) >>
      â€˜b - y = -(y-b)â€™ by simp[] >> simp[]) >>
  simp[SUBSET_DEF, PULL_EXISTS] >>
  qx_genl_tac [â€˜xâ€™, â€˜yâ€™] >> simp[Abbrâ€˜râ€™] >>
  simp[REAL_LT_SUB_LADD] >> strip_tac >>
  qsuff_tac â€˜sqrt (x pow 2 + y pow 2) < sqrt (1)â€™
  >- (strip_tac >>
      drule_at Any REAL_POW_LT2 >>
      disch_then $ qspec_then â€˜2â€™ MP_TAC >> simp[] >>
      simp[SQRT_POS_LE,REAL_LE_ADD,SQRT_POW_2] ) >>
  simp[SQRT_1] >>
  irule REAL_LET_TRANS >>
  FIRST_ASSUM $ irule_at Any >>
  gs[tri_ineq] >>
  FIRST_X_ASSUM $ qspecl_then [â€˜xâ€™,â€˜yâ€™,â€˜aâ€™,â€˜bâ€™,â€˜0â€™,â€˜0â€™] mp_tac >>
                simp[]
QED

Theorem exercise_2_2_1_ii:
  tri_ineq â‡’
  D = BIGUNION {R a b | (a,b) âˆˆ D}
Proof
  strip_tac >>
  irule SUBSET_ANTISYM >>
  reverse conj_tac
  >- simp[BIGUNION_SUBSET,PULL_EXISTS,exercise_2_2_1_i] >>
  simp[SUBSET_DEF, PULL_EXISTS, FORALL_PROD] >>
  rw[] >> rename [`(x, y) âˆˆ D`] >> qexistsl_tac [`x`, `y`] >>
  simp[R_includes_centre]
QED

val euclidean_2_def =
    new_specification ("euclidean_2_def", ["euclidean_2"], open_rectangle_topology_exists)

Theorem open_rectangle_open =
            euclidean_2_def |> cj 2 |> ONCE_REWRITE_RULE [basis_def] |> cj 1 |>
            SIMP_RULE (srw_ss ()) [open_rectangles, PULL_EXISTS]

Theorem sqrt_lt:
  âˆ€x. 0 â‰¤ x â‡’ (sqrt x < y â‡” x < y pow 2 âˆ§ 0 < y)
Proof
  rw[EQ_IMP_THM] (* 3 *)
  >- (drule_at_then (Pos last) (qspec_then â€˜2â€™ mp_tac) REAL_POW_LT2 >>
     simp[SQRT_POW_2,SQRT_POS_LE])
  >- (irule REAL_LET_TRANS >> qexists_tac â€˜sqrt xâ€™ >> simp[SQRT_POS_LE])
  >- (qspecl_then [â€˜xâ€™,â€˜y pow 2â€™] mp_tac SQRT_MONO_LT >>
      simp[POW_2_SQRT])
QED

Theorem exercise_2_2_1_iii:
  tri_ineq â‡’
  open_in euclidean_2 D
Proof
  simp[Once exercise_2_2_1_ii] >> strip_tac >>
  irule OPEN_IN_BIGUNION >> rw[] >>
  rw[R_def] >> irule open_rectangle_open >> rw[]
  >> irule (realLib.REAL_ARITH ``(0:real) < x â‡’ y - x < y + x``) >>
  gs[D_def,REAL_SUB_LT] >> simp[REAL_LE_ADD,sqrt_lt]
QED

Theorem exercise_2_2_3:
  basis {ival a b | a < b âˆ§ rational a âˆ§ rational b} euclidean
Proof
  rw[basis_def] (* 2 *)
  >- simp[ivals_open]
  >- (fs[open_in_euclidean] >>
      â€˜âˆ€x. x âˆˆ s â‡’ âˆƒa b. rational a âˆ§ rational b âˆ§ x âˆˆ ival a b âˆ§ ival a b âŠ† sâ€™
        by
        (rw[] >> first_x_assum drule >> strip_tac >>
         â€˜a < x âˆ§ x < bâ€™ by gs[ival_def] >>
         â€˜âˆƒa' b'. a < a' âˆ§ a' < x âˆ§ x < b' âˆ§ b' < b âˆ§ rational a' âˆ§ rational b'â€™
           by metis_tac[rationals_dense] >>
         qexistsl_tac [â€˜a'â€™,â€˜b'â€™] >> simp[] >> gs[SUBSET_DEF,ival_def]) >>
      gs[SKOLEM_THM,GSYM RIGHT_EXISTS_IMP_THM] >>
      rename [â€˜rational (lb _)â€™,â€˜rational (ub _)â€™,â€˜ival (lb _) (ub _)â€™] >>
      qexists_tac â€˜{ival (lb x) (ub x)| x âˆˆ s}â€™ >> conj_tac (* 2 *)
      >- (rw[SUBSET_DEF] >>
          â€˜âˆ€a. a âˆˆ s â‡’ lb a < ub aâ€™ suffices_by metis_tac[] >>
          rpt strip_tac >> first_x_assum drule >> simp[ival_def])
      >- (rw[EQ_IMP_THM,Once EXTENSION] (* 2 *)
         >- (simp[PULL_EXISTS] >> metis_tac[SUBSET_DEF])
         >- metis_tac[SUBSET_DEF]))
QED

Theorem countable_integer[simp]:
  countable univ(:int)
Proof
  simp[countable_def] >>
  qexists_tac â€˜Î»i. if i < 0 then Num(-i)*2 + 1 else Num(i)*2â€™ >>
  rw[INJ_DEF] >>
  rename [â€˜i = jâ€™] >>
  Cases_on â€˜iâ€™ >> gs[] >>
  Cases_on â€˜jâ€™ >> gs[] >>
  metis_tac[ODD_EVEN,ODD_EXISTS,ADD1,EVEN_EXISTS]
QED

Theorem countable_rational[simp]:
  countable rational
Proof
  simp[countable_def] >>
  â€˜SURJ (Î»(i,j). real_of_int i / real_of_int j)
        (univ(:int) CROSS univ(:int))
        rationalâ€™
    by (rw[SURJ_DEF,FORALL_PROD,EXISTS_PROD,IN_DEF,rational_def]
        >- (Cases_on â€˜p_2 = 0â€™ >> simp[]
            >- (simp[real_div] >> qexistsl_tac [â€˜0â€™,â€˜1â€™] >>
                simp[]) >>
            metis_tac[]) >>
        metis_tac[]) >>
  dxrule_then strip_assume_tac SURJ_INJ_INV >>
  pop_assum kall_tac >>
  dxrule_then (irule_at Any) INJ_COMPOSE >>
  simp[GSYM countable_def] >>
  irule(cross_countable) >>
  simp[]
QED

Theorem exercise_2_2_4_i:
  second_countable euclidean
Proof
  rw[second_countable_def] >>
  irule_at Any exercise_2_2_3 >>
  simp[countable_def] >>
  â€˜INJ (Î»i. @(a,b). a < b âˆ§ i = ival a b)
      {ival a b | a < b âˆ§ rational a âˆ§ rational b}
      {(a,b) | rational a âˆ§ rational b}â€™
    by (rw[INJ_DEF]
        >- (qexistsl_tac [â€˜aâ€™,â€˜bâ€™] >>
            simp[] >> SELECT_ELIM_TAC >>
            csimp[EXISTS_PROD,FORALL_PROD]) >>
        pop_assum mp_tac >>
        csimp[] >> SELECT_ELIM_TAC >> simp[EXISTS_PROD,FORALL_PROD] >>
        SELECT_ELIM_TAC >> simp[EXISTS_PROD,FORALL_PROD]) >>
  dxrule_then (irule_at Any) INJ_COMPOSE >>
  simp[GSYM countable_def] >>
  â€˜{(a,b) | rational a âˆ§ rational b} = rational Ã— rationalâ€™
    by rw[EXTENSION,FORALL_PROD,IN_DEF] >>
  pop_assum SUBST1_TAC >>
  irule(cross_countable) >> simp[]
QED

Theorem open_sets_form_basis:
  basis { s | open_in t s } t
Proof
  simp[basis_def] >> rpt strip_tac >>
  qexists_tac â€˜{s}â€™ >> simp[]
QED

Theorem exercise_2_2_4_iv:
  second_countable (finite_closed_topology ð•Œ(:int))
Proof
  simp[second_countable_def] >>
  irule_at (Pos hd) open_sets_form_basis >>
  simp[finite_closed_open_sets] >> simp[GSPEC_OR] >>
  â€˜{s | FINITE (ð•Œ(:int) DIFF s) } â‰ˆ
    { s:int set | FINITE s âˆ§ s âŠ† UNIV}â€™
    by (simp[cardeq_def] >> qexists_tac â€˜COMPLâ€™ >>
        simp[BIJ_DEF, INJ_DEF, SURJ_DEF, COMPL_DEF] >>
        rpt strip_tac >- gs[EXTENSION] >>
        qexists_tac â€˜UNIV DIFF xâ€™ >> simp[GSYM COMPL_DEF]) >>
  drule_then irule (iffRL countable_cardeq) >>
  resolve_then Any irule
               finite_subsets_bijection
               (iffLR countable_cardeq) >>
  simp[]
QED

Theorem bigunion_cross:
  BIGUNION as CROSS BIGUNION bs = BIGUNION {a CROSS b | a âˆˆ as âˆ§ b âˆˆ bs}
Proof
  simp[Once EXTENSION, FORALL_PROD, PULL_EXISTS] >> metis_tac[]
QED

Theorem exercise_2_2_6:
  basis B1 t1 âˆ§ basis B2 t2 â‡’
  âˆƒt. topspace t = topspace t1 CROSS topspace t2 âˆ§
      basis {b1 CROSS b2 | b1 âˆˆ B1 âˆ§ b2 âˆˆ B2} t
Proof
  strip_tac >> simp[prop_2_2_8] >> rpt strip_tac
  >- (simp[Once EXTENSION, FORALL_PROD, PULL_EXISTS] >>
      metis_tac[IN_BIGUNION, prop_2_2_8])
  >- (rw[] >> simp[INTER_CROSS] >>
      rename[â€˜(a1 âˆ© a2) CROSS (b1 âˆ© b2)â€™, â€˜a1 âˆˆ Aâ€™, â€˜b1 âˆˆ Bâ€™]
      >> â€˜âˆƒas. as âŠ† A âˆ§ a1 âˆ© a2 = BIGUNION asâ€™ by metis_tac [prop_2_2_8]
      >> â€˜âˆƒbs. bs âŠ† B âˆ§ b1 âˆ© b2 = BIGUNION bsâ€™ by metis_tac [prop_2_2_8]
      >> simp[bigunion_cross] >> irule_at Any EQ_REFL >> rw[SUBSET_DEF]
      >> metis_tac[SUBSET_DEF])
QED


(*
Let B be the collection of all half-open intervals of the form (a, b], a < b, where (a, b] = {x : x âˆˆ R, a < x ô°„ b}. Then B is a basis for a topology on R, since R is the union of all members of B and the intersection of any two half-open intervals is a half-open interval.
*)

Definition lhalf_open_ival:
lhalf_open_ival a b = {x| a < x âˆ§ x â‰¤ b}
End

Theorem example_2_3_1:
  âˆƒt. topspace t = UNIV âˆ§ basis {lhalf_open_ival a b | a < b} t âˆ§ t â‰  euclidean
Proof
  â€˜âˆƒt. topspace t = UNIV âˆ§ basis {lhalf_open_ival a b | a < b} tâ€™
    by
    (simp[prop_2_2_8] >> rpt strip_tac (* 2 *)
     >- (rw[Once EXTENSION,PULL_EXISTS] >> qexistsl_tac [â€˜x - 1â€™,â€˜xâ€™] >> simp[lhalf_open_ival]) >>
     rw[SUBSET_DEF] >> rename[â€˜lhalf_open_ival a b âˆ© lhalf_open_ival c dâ€™] >>
     wlog_tac â€˜a â‰¤ câ€™ [â€˜aâ€™,â€˜bâ€™,â€˜câ€™,â€˜dâ€™] (* 2 *)
     >- metis_tac[INTER_COMM,RealArith.REAL_ARITH â€œÂ¬(a:real â‰¤ c) â‡’ c â‰¤ aâ€] >>
     Cases_on â€˜b â‰¤ câ€™ (* 2 *)
     >- (qexists_tac â€˜{}â€™ >> rw[] >> simp[EXTENSION,lhalf_open_ival]) >>
     qexists_tac â€˜{lhalf_open_ival c (min b d)}â€™ >> simp[] >> strip_tac (* 2 *)
     >- (qexistsl_tac [â€˜câ€™,â€˜min b dâ€™] >> rw[min_def]) >>
     rw[min_def,lhalf_open_ival,EXTENSION]) >>
  qexists_tac â€˜tâ€™ >> simp[TOPOLOGY_EQ] >> qexists_tac â€˜lhalf_open_ival 0 1â€™ >>
  gs[basis_def,PULL_EXISTS] >> simp[lhalf_open_ival,exercise_2_1_1]
QED


val _ = export_theory();
